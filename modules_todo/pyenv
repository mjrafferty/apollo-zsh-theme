# vim:ft=zsh
################################################################
# Segment to display pyenv information
# https://github.com/pyenv/pyenv#choosing-the-python-version

__apollo_read_pyenv_version_file() {

  [[ -r $1 ]] || return

  local content

  read -rd $'\0' content <$1 2>/dev/null

  __APOLLO_RETURN_MESSAGE=${${(j.:.)${(@)${=content}#python-}:-system}}

}

__apollo_pyenv_global_version() {

  __apollo_read_pyenv_version_file ${PYENV_ROOT:-$HOME/.pyenv}/version || __APOLLO_RETURN_MESSAGE=system

}

prompt_pyenv() {

  local v dir

  v=${(j.:.)${(@)${(s.:.)PYENV_VERSION}#python-}}

  if [[ -z $v ]]; then

    if [[ $PYENV_DIR == /* ]]; then
      dir=$PYENV_DIR
    else
      dir="$PWD/$PYENV_DIR"
    fi

    while true; do

      if __apollo_read_pyenv_version_file $dir/.python-version; then
        v=$__APOLLO_RETURN_MESSAGE
        break
      fi

      if [[ $dir == / ]]; then

        if [[ $APOLLO_PYENV_PROMPT_ALWAYS_SHOW != true ]]; then
          return;
        fi

        __apollo_pyenv_global_version
        v=$__APOLLO_RETURN_MESSAGE

        break

      fi

      dir=${dir:h}

    done
  fi

  if [[ $APOLLO_PYENV_PROMPT_ALWAYS_SHOW == false ]]; then
    __apollo_pyenv_global_version
    [[ $v == $__APOLLO_RETURN_MESSAGE ]] && return
  fi

  "$1_prompt_module" "$0" "$2" "blue" "$DEFAULT_COLOR" 'PYTHON_ICON' 0 '' "${v//\%/%%}"

}
