# vim:ft=bash
# Almost the same as `nvm_version default` but faster. The differences shouldn't affect
# the observable behavior of Powerlevel10k.
_apollo_nvm_ls_default() {
  local v=default
  local -a seen
  seen=($v)
  local target
  while [[ -r $NVM_DIR/alias/$v ]] && read target <$NVM_DIR/alias/$v; do
    [[ -n $target && ${seen[(I)$target]} == 0 ]] || return
    seen+=$target
    v=$target
  done

  case $v in
    default|N/A)
      return 1
    ;;
    system|v)
      _APOLLO_RETVAL=system
      return
    ;;
    iojs-[0-9]*)
      v=iojs-v${v#iojs-}
    ;;
    [0-9]*)
      v=v$v
    ;;
  esac

  if [[ $v == v*.*.* ]]; then
    if [[ -x $NVM_DIR/versions/node/$v/bin/node || -x $NVM_DIR/$v/bin/node ]]; then
      _APOLLO_RETVAL=$v
      return
    elif [[ -x $NVM_DIR/versions/io.js/$v/bin/node ]]; then
      _APOLLO_RETVAL=iojs-$v
      return
    else
      return 1
    fi
  fi

  local -a dirs
  case $v in
    node|node-|stable)
      dirs=($NVM_DIR/versions/node $NVM_DIR)
      v='(v[1-9]*|v0.*[02468].*)'
    ;;
    unstable)
      dirs=($NVM_DIR/versions/node $NVM_DIR)
      v='v0.*[13579].*'
    ;;
    iojs*)
      dirs=($NVM_DIR/versions/io.js)
      v=v${${${v#iojs}#-}#v}'*'
    ;;
    *)
      dirs=($NVM_DIR/versions/node $NVM_DIR $NVM_DIR/versions/io.js)
      v=v${v#v}'*'
    ;;
  esac

  local -a matches
  matches=(${^dirs}/${~v}(/N))
  (( $#matches )) || return

  emulate -L zsh && setopt extendedglob

  local max path
  local -a match
  for path in ${(Oa)matches}; do
    [[ ${path:t} == (#b)v(*).(*).(*) ]] || continue
    v=${(j::)${(@l:6::0:)match}}
    [[ $v > $max ]] || continue
    max=$v
    _APOLLO_RETVAL=${path:t}
    [[ ${path:h:t} != io.js ]] || _APOLLO_RETVAL=iojs-$_APOLLO_RETVAL
  done

  [[ -n $max ]]
}

# The same as `nvm_version current` but faster.
_apollo_nvm_ls_current() {
  local node_path=${commands[node]:A}
  [[ -n $node_path ]] || return

  local nvm_dir=${NVM_DIR:A}
  if [[ -n $nvm_dir && $node_path == $nvm_dir/versions/io.js/* ]]; then
    _apollo_cached_cmd_stdout iojs --version || return
    _APOLLO_RETVAL=iojs-v${_APOLLO_RETVAL#v}
  elif [[ -n $nvm_dir && $node_path == $nvm_dir/* ]]; then
    _apollo_cached_cmd_stdout node --version || return
    _APOLLO_RETVAL=v${_APOLLO_RETVAL#v}
  else
    _APOLLO_RETVAL=system
  fi
}

################################################################
# Segment to display Node version from NVM
# Only prints the module if different than the default value
prompt_nvm() {
  [[ -n $NVM_DIR ]] && _apollo_nvm_ls_current || return
  local current=$_APOLLO_RETVAL
  ! _apollo_nvm_ls_default || [[ $_APOLLO_RETVAL != $current ]] || return
  $1_prompt_module "$0" "$2" "magenta" "black" 'NODE_ICON' 0 '' "${${current#v}//\%/%%}"
}
