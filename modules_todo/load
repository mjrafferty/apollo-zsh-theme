# vim:ft=zsh
################################################################
# Segment to display load
prompt_load() {
  local bucket=2
  case $APOLLO_LOAD_WHICH in
    1) bucket=1;;
    5) bucket=2;;
    15) bucket=3;;
  esac

  local load
  case $OS in
    OSX|BSD)
      (( $+commands[sysctl] )) || return
      load=$(command sysctl -n vm.loadavg 2>/dev/null) || return
      load=${${(A)=load}[bucket+1]//,/.}
    ;;
    *)
      [[ -r /proc/loadavg ]] || return
      __apollo_read_file /proc/loadavg || return
      load=${${(A)=__APOLLO_RETVAL}[bucket]//,/.}
    ;;
  esac

  if (( ! $+__APOLLO_NUM_CPUS )); then
    case $OS in
      OSX) (( $+commands[sysctl] )) && __APOLLO_NUM_CPUS=$(command sysctl -n hw.logicalcpu 2>/dev/null) || return;;
      BSD) (( $+commands[sysctl] )) && __APOLLO_NUM_CPUS=$(command sysctl -n hw.ncpu 2>/dev/null) || return;;
      *)   (( $+commands[nproc]  )) && __APOLLO_NUM_CPUS=$(command nproc 2>/dev/null) || return;;
    esac
  fi

  if (( load > 0.7 * __APOLLO_NUM_CPUS )); then
    local state=critical bg=red
  elif (( load > 0.5 * __APOLLO_NUM_CPUS )); then
    local state=warning bg=yellow
  else
    local state=normal bg=green
  fi

  $1_prompt_module $0_$state $2 $bg "$DEFAULT_COLOR" LOAD_ICON 0 '' $load
}
