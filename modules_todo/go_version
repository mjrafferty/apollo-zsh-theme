# vim:ft=zsh
################################################################
# GO prompt
prompt_go_version() {
  __apollo_cached_cmd_stdout go version || return
  emulate -L zsh && setopt extendedglob
  local -a match
  [[ $__APOLLO_RETVAL == (#b)*(go[[:digit:].]##)* ]] || return
  local v=$match[1]
  local p=$GOPATH
  if [[ -z $p ]]; then
    if [[ -d $HOME/go ]]; then
      p=$HOME/go
    else
      p=$(command go env GOPATH 2>/dev/null) && [[ -n $p ]] || return
    fi
  fi
  if [[ $PWD/ != $p/* ]]; then
    local dir=$PWD
    while [[ ! -e $dir/go.mod ]]; do
      [[ $dir == / ]] && return
      dir=${dir:h}
    done
  fi
  "$1_prompt_module" "$0" "$2" "green" "grey93" "GO_ICON" 0 '' "${v//\%/%%}"
}
