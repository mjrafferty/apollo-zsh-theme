# vim:ft=zsh
__apollo_read_rbenv_version_file() {
  [[ -r $1 ]] || return
  local content
  read -r content <$1 2>/dev/null
  __APOLLO_RETVAL="${${(A)=content}[1]}"
  [[ -n $__APOLLO_RETVAL ]]
}

__apollo_rbenv_global_version() {
  __apollo_read_rbenv_version_file ${RBENV_ROOT:-$HOME/.rbenv}/version || __APOLLO_RETVAL=system
}

################################################################
# Segment to display rbenv information
# https://github.com/rbenv/rbenv#choosing-the-ruby-version
prompt_rbenv() {
  local v=$RBENV_VERSION
  if [[ -z $v ]]; then
    [[ $RBENV_DIR == /* ]] && local dir=$RBENV_DIR || local dir="$PWD/$RBENV_DIR"
    while true; do
      if __apollo_read_rbenv_version_file $dir/.ruby-version; then
        v=$__APOLLO_RETVAL
        break
      fi
      if [[ $dir == / ]]; then
        [[ $APOLLO_RBENV_PROMPT_ALWAYS_SHOW == true ]] || return
        __apollo_rbenv_global_version
        v=$__APOLLO_RETVAL
        break
      fi
      dir=${dir:h}
    done
  fi

  if [[ $APOLLO_RBENV_PROMPT_ALWAYS_SHOW == false ]]; then
    __apollo_rbenv_global_version
    [[ $v == $__APOLLO_RETVAL ]] && return
  fi

  "$1_prompt_module" "$0" "$2" "red" "$DEFAULT_COLOR" 'RUBY_ICON' 0 '' "${v//\%/%%}"
}
