# vim:ft=zsh

__apollo_run_module() {

  local module side cache_key always_show mode
  local -i line num

  line="$1"
  side="$2"
  module="$3"
  num="$4"
  cache_key="$5"

  context=":apollo:${APOLLO_THEME}:${line}:${side}:${module}"

  # Clear out old message before passing to module
  __APOLLO_RETURN_MESSAGE=""

  if ((${+functions[__apollo_${module}_run]})); then

    "__apollo_${module}_run" "${context}" "${__APOLLO_ASYNC_RESULTS[$module]}" >&"${__APOLLO_BUFFER_FD}"

    # Flush any remaining data in buffer
    sysread -i "${__APOLLO_BUFFER_FD}" -t 0
    unset REPLY

  else
    __APOLLO_RETURN_MESSAGE="${__APOLLO_ASYNC_RESULTS[$module]}"
  fi

  mode="${__APOLLO_MODULE_MODE[$module]}"

  if [[ ${#__APOLLO_RETURN_MESSAGE} == 0 ]]; then
    zstyle -s "${context}:${mode}:main" text __APOLLO_RETURN_MESSAGE
    printf -v __APOLLO_RETURN_MESSAGE "%b" "$__APOLLO_RETURN_MESSAGE"
  fi

  zstyle -b "${context}:${mode}" always_show always_show

  if [[ "$always_show" == "no" && ${#__APOLLO_RETURN_MESSAGE} == 0 ]]; then
    __APOLLO_MODULE_HAS_LENGTH[$num]="0"
    __APOLLO_MODULE_TEXT[$num]=""
    if [[ -n "$cache_key" ]]; then
      __APOLLO_MODULE_CACHE["${cache_key}"]="0\0${__APOLLO_RETURN_MESSAGE}"
    fi
    return;
  fi

  __apollo_style_module "${context}:${mode}" "$__APOLLO_RETURN_MESSAGE"

  __APOLLO_MODULE_TEXT[$num]="${__APOLLO_RETURN_MESSAGE}"

  __APOLLO_MODULE_HAS_LENGTH[$num]="1"

  if [[ -n "$cache_key" ]]; then
    __APOLLO_MODULE_CACHE["${cache_key}"]="${__APOLLO_MODULE_HAS_LENGTH[$num]}\0${__APOLLO_MODULE_TEXT[$num]}\0${mode}"
  fi

}

__apollo_run_module "$@"
