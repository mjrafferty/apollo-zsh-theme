# vim:ft=zsh

__apollo_run_module() {

  local module side cache_key bg_color
  local -i line num
  local always_show module_colors
  local left_label right_label
  local left_surround right_surround
  local left_padding right_padding
  local -i width min_width

  line="$1"
  side="$2"
  module="$3"
  num="$4"
  cache_key="$5"

  context=":apollo:${APOLLO_THEME}:${line}:${side}:${module}"

  __APOLLO_MODULE_MODE[$module]="default"

  # Clear out old message before passing to module
  __APOLLO_RETURN_MESSAGE=""

  "__apollo_${module}_run" "${context}" "${__APOLLO_ASYNC_RESULTS[$module]}" >&"${__APOLLO_BUFFER_FD}"

  # Flush any remaining data in buffer
  while read -rt dump <&"${__APOLLO_BUFFER_FD}"; do true; done

  mode="${__APOLLO_MODULE_MODE[$module]}"

  zstyle -b "${context}" always_show always_show

  if [[ "$always_show" == "no" && ${#__APOLLO_RETURN_MESSAGE} == 0 ]]; then
    __APOLLO_MODULE_HAS_LENGTH[$num]="0"
    __APOLLO_MODULE_TEXT[$num]=""
    if [[ -n "$cache_key" ]]; then
      __APOLLO_MODULE_CACHE["${cache_key}"]="0\0${__APOLLO_RETURN_MESSAGE}"
    fi
    return;
  fi

  __apollo_style_module "${context}:${mode}" "$__APOLLO_RETURN_MESSAGE"

  __APOLLO_MODULE_TEXT[$num]="${__APOLLO_RETURN_MESSAGE}"

  __APOLLO_MODULE_HAS_LENGTH[$num]="1"

  if [[ -n "$cache_key" ]]; then
    __APOLLO_MODULE_CACHE["${cache_key}"]="${__APOLLO_MODULE_HAS_LENGTH[$num]}\0${__APOLLO_MODULE_TEXT[$num]}\0${mode}"
  fi

}

__apollo_run_module "$@"
