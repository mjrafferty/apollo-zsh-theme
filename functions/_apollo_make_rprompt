# vim:ft=zsh

_apollo_make_rprompt(){

  local prompt_length current_module line_index
  local -a left_modules right_modules

  zstyle -a 'apollo:core:modules:left' modules left_modules
  zstyle -a 'apollo:core:modules:right' modules right_modules

  line_index="$1"

  prompt_length="${#right_modules[@]}"

  _APOLLO_RETURN_MESSAGE=""

  while ((_APOLLO_CURRENT_RPROMPT <= prompt_length)); do

    current_module="${right_modules[_APOLLO_CURRENT_RPROMPT]}"

    case "${current_module}" in
      "newline")
        ((_APOLLO_CURRENT_RPROMPT++));
        break;
        ;;
      *)
        _APOLLO_RETURN_MESSAGE+="\${_APOLLO_RIGHT_SEPARATORS[$_APOLLO_CURRENT_RPROMPT]}\${_APOLLO_MODULE_TEXT[$current_module]}"
        _APOLLO_RIGHT_HAS_LENGTH[line_index]+=" + \$_APOLLO_MODULE_HAS_LENGTH[$current_module]"
        ((_APOLLO_CURRENT_RPROMPT++));
        ;;
    esac

  done

}

_apollo_make_rprompt "$@"
