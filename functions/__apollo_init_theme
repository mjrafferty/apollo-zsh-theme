# vim:ft=zsh

__apollo_init_theme() {

  local module theme
  local -a left_modules right_modules

  theme="${APOLLO_THEME}"

  zstyle -a ":apollo:${APOLLO_THEME}:core:modules:left" modules left_modules
  zstyle -a ":apollo:${APOLLO_THEME}:core:modules:right" modules right_modules

  # Load enabled modules
  for module in "${left_modules[@]}" "${right_modules[@]}"; do

    if [[ "$module" == "newline" || "$module" == "ruler" || "${__APOLLO_MODULES_LOADED[$module]}" == 1 ]]; then
      continue;
    fi

    autoload -Uz +X "__apollo_${module}_load" \
      && "__apollo_${module}_load"

    if (( ${+functions[__apollo_${module}_async]} || ${+functions[__apollo_${module}_always_async]} )); then

      if ((__APOLLO_ASYNC != 1)); then

        # Remove hooks so async worker doesn't run them
        zle -D zle-line-finish
        add-zsh-hook -d precmd __apollo_precmd
        add-zsh-hook -d preexec __apollo_preexec
        add-zsh-hook -d chpwd __apollo_chpwd
        add-zsh-hook -d zshexit __apollo_cleanup

        if ((!${+functions[async_start_worker]})); then
          # If async library already present, use it. Else use bundled
          if ((${+functions[async_init]})); then
            async_init
          else
            autoload -Uz __apollo_lib_async_load \
              && __apollo_lib_async_load
          fi
        fi

        async_start_worker apollo_async_helper -n

        # Add hooks back in
        zle -N zle-line-finish
        add-zsh-hook precmd __apollo_precmd
        add-zsh-hook preexec __apollo_preexec
        add-zsh-hook chpwd __apollo_chpwd
        add-zsh-hook zshexit __apollo_cleanup

        __APOLLO_ASYNC=1

      else
        async_worker_eval apollo_async_helper "autoload -Uz +X __apollo_${module}_load && __apollo_${module}_load"
      fi

      if ((${+functions[__apollo_${module}_init_async]})); then
        async_worker_eval apollo_async_helper "__apollo_${module}_init_async"
      fi

    fi

    __APOLLO_MODULES_LOADED[$module]=1

  done

  typeset -gi __APOLLO_RPROMPT_MODULE_INDEX=1

  __apollo_build_left_prompt
  __apollo_build_right_prompt "${__APOLLO_PROMPT_THEME_END[$APOLLO_THEME]}"

  unset __APOLLO_RPROMPT_MODULE_INDEX

  __APOLLO_FINISHED_RPROMPTS[$APOLLO_THEME]="${__APOLLO_RETURN_MESSAGE}\${__APOLLO_RIGHT_LINK[${__APOLLO_PROMPT_THEME_END[$APOLLO_THEME]}]}"

}
