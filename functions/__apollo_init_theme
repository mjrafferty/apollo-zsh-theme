# vim:ft=zsh

__apollo_init_theme() {

  local module theme
  local -a left_modules right_modules

  theme="${__APOLLO_PROMPT_THEME}"

  zstyle -a ":apollo:${__APOLLO_PROMPT_THEME}:core:modules:left" modules left_modules
  zstyle -a ":apollo:${__APOLLO_PROMPT_THEME}:core:modules:right" modules right_modules

  # Load enabled modules
  for module in "${left_modules[@]}" "${right_modules[@]}"; do

    if [[ "$module" == "newline" || "$module" == "ruler" || "${__APOLLO_MODULES_LOADED[$module]}" == 1 ]]; then
      continue;
    fi

    autoload -Uz +X "__apollo_${module}_load" \
      && "__apollo_${module}_load"

    if (( ${+functions[__apollo_${module}_async]} || ${+functions[__apollo_${module}_always_async]} )); then

      if ((__APOLLO_ASYNC != 1)); then

        if ((!${+functions[async_start_worker]})); then
          # If async library already present, use it. Else use bundled
          if ((${+functions[async_init]})); then
            async_init
          else
            autoload -Uz +X __apollo_lib_async_load \
              && __apollo_lib_async_load
          fi
        fi

        async_start_worker apollo_async_helper -u -n

        async_worker_eval apollo_async_helper "zle -D zle-line-finish"
        async_worker_eval apollo_async_helper "add-zsh-hook -d precmd __apollo_precmd"
        async_worker_eval apollo_async_helper "add-zsh-hook -d preexec __apollo_preexec"
        async_worker_eval apollo_async_helper "add-zsh-hook -d chpwd __apollo_chpwd"
        async_worker_eval apollo_async_helper "add-zsh-hook -d zshexit __apollo_cleanup"

        __APOLLO_ASYNC=1

      else
        async_worker_eval apollo_async_helper "autoload -Uz +X __apollo_${module}_load && __apollo_${module}_load"
      fi

      if ((${+functions[__apollo_${module}_init_async]})); then
        async_worker_eval apollo_async_helper "__apollo_${module}_init_async"
      fi

    fi

    __APOLLO_MODULES_LOADED[$module]=1

  done

  typeset -gi __APOLLO_RPROMPT_MODULE_INDEX=1

  __apollo_build_left_prompt
  __apollo_build_right_prompt "${__APOLLO_PROMPT_THEME_END[$__APOLLO_PROMPT_THEME]}"

  unset __APOLLO_RPROMPT_MODULE_INDEX

  __APOLLO_FINISHED_RPROMPTS[$__APOLLO_PROMPT_THEME]="${__APOLLO_RETURN_MESSAGE}\${__APOLLO_RIGHT_LINK[${__APOLLO_PROMPT_THEME_END[$__APOLLO_PROMPT_THEME]}]}"

}
