# vim:ft=zsh

__apollo_init_theme() {

  local module theme
  local -a left_modules right_modules

  theme="${APOLLO_THEME}"

  zstyle -a ":apollo:${APOLLO_THEME}:core:modules:left" modules left_modules
  zstyle -a ":apollo:${APOLLO_THEME}:core:modules:right" modules right_modules

  # Load enabled modules
  for module in "${left_modules[@]}" "${right_modules[@]}"; do

    if [[ "$module" == "newline" || "$module" == "ruler" || "${__APOLLO_MODULES_LOADED[$module]}" == 1 ]]; then

      if ((${+functions[__apollo_${module}_theme_init]})); then
        __apollo_${module}_theme_init
      fi

      continue;
    fi

    autoload -Uz +X "__apollo_${module}_load" 2> /dev/null \
      && "__apollo_${module}_load"

    __APOLLO_MODULES_LOADED[$module]=1

    if (( ${+functions[__apollo_${module}_async]} || ${+functions[__apollo_${module}_always_async]} )); then

      if ((__APOLLO_ASYNC != 1)); then

        __apollo_start_async
        __APOLLO_ASYNC=1

      else

        async_worker_eval apollo_async_helper "autoload -Uz +X __apollo_${module}_load && __apollo_${module}_load"

        async_worker_eval apollo_async_helper "__apollo_${module}_async_runner () { __apollo_async_runner ${module} \"\${@}\" }"

        if ((${+functions[__apollo_${module}_init_async]})); then
          async_worker_eval apollo_async_helper "__apollo_${module}_init_async"
        fi

      fi

    fi

    if ((${+functions[__apollo_${module}_theme_init]})); then
      __apollo_${module}_theme_init
    fi


  done

  typeset -gi __APOLLO_RPROMPT_MODULE_INDEX=1

  __apollo_build_left_prompt
  __apollo_build_right_prompt "${__APOLLO_PROMPT_THEME_END[$APOLLO_THEME]}"

  unset __APOLLO_RPROMPT_MODULE_INDEX

  __APOLLO_FINISHED_RPROMPTS[$APOLLO_THEME]="${__APOLLO_RETURN_MESSAGE}\${__APOLLO_RIGHT_LINK[${__APOLLO_PROMPT_THEME_END[$APOLLO_THEME]}]}"

}
