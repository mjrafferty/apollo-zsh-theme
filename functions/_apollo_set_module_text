# vim:ft=zsh

_apollo_set_module_text() {

  local module module_text modules_colors transition_fg cache_key side
  local -u fg_var bg_var

  module="$1"
  side="$2"
  cache_key="$3"

  fg_var="APOLLO_${module}_FG"
  bg_var="APOLLO_${module}_BG"

  # Clear out old message before passing to module
  _APOLLO_RETURN_MESSAGE=""

   "_apollo_${module}_run" >&"${_APOLLO_BUFFER_FD}"

  # Flush any remaining data in buffer
  while read -rt dump <&"${_APOLLO_BUFFER_FD}"; do true; done

  if (( ${#_APOLLO_RETURN_MESSAGE} == 0 )); then
    _APOLLO_MODULE_HAS_LENGTH[$module]="0"
    _APOLLO_MODULE_TEXT[$module]=""
    if [[ -n "$cache_key" ]]; then
      _APOLLO_CACHE["${module}-${cache_key}"]="0|${_APOLLO_RETURN_MESSAGE}"
    fi
    return;
  fi

  ## TODO padding
  ## TODO icons?
  module_text=" ${_APOLLO_RETURN_MESSAGE} "

  _apollo_set_colors "${(P)${fg_var}}" "${(P)${bg_var}}"
  module_colors="${_APOLLO_RETURN_MESSAGE}"

  _apollo_set_colors "${(P)${bg_var}}" "keep"
  transition_fg="${_APOLLO_RETURN_MESSAGE}"

  _APOLLO_MODULE_TEXT[$module]="${module_colors}${module_text}${transition_fg}"

  _APOLLO_MODULE_HAS_LENGTH[$module]="1"

  if [[ -n "$cache_key" ]]; then
    _APOLLO_CACHE["${module}-${cache_key}"]="${_APOLLO_MODULE_HAS_LENGTH[$module]}|${_APOLLO_MODULE_TEXT[$module]}"
  fi

}

_apollo_set_module_text "$@"
