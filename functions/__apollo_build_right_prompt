# vim:ft=zsh

__apollo_build_right_prompt(){

  local current_module line_index
  local -a left_modules right_modules

  zstyle -a ':apollo:core:modules:left' modules left_modules
  zstyle -a ':apollo:core:modules:right' modules right_modules

  line_index="$1"

  __APOLLO_RETURN_MESSAGE="\${__APOLLO_RIGHT_BEGIN[$line_index]}"

  while ((__APOLLO_CURRENT_RPROMPT <= ${#right_modules[@]} )); do

    current_module="${right_modules[__APOLLO_CURRENT_RPROMPT]}"

    case "${current_module}" in
      "newline")
        ((__APOLLO_CURRENT_RPROMPT++));
        break;
        ;;
      *)
        __APOLLO_MODULE_MAP[__APOLLO_MODULE_INDEX]="$current_module"
        __APOLLO_REVERSE_MODULE_MAP[$current_module]+="${line_index}:right:${__APOLLO_MODULE_INDEX}\0"
        __APOLLO_RIGHT_LINE_MAP[line_index]+="${__APOLLO_MODULE_INDEX}\0"

        __APOLLO_RETURN_MESSAGE+="\${__APOLLO_RIGHT_SEPARATORS[$__APOLLO_MODULE_INDEX]}\${(e)__APOLLO_MODULE_TEXT[$__APOLLO_MODULE_INDEX]}"
        __APOLLO_RIGHT_HAS_LENGTH[line_index]+=" + \$__APOLLO_MODULE_HAS_LENGTH[$__APOLLO_MODULE_INDEX]"
        ((__APOLLO_MODULE_INDEX++));
        ((__APOLLO_CURRENT_RPROMPT++));
        ;;
    esac

  done

  __APOLLO_RETURN_MESSAGE+="\${__APOLLO_RIGHT_END[$line_index]}"

}

__apollo_build_right_prompt "$@"
