# vim:ft=zsh

_apollo_add_separators() {

  local current_module
  local -i first_skipped=0
  local -u bg_var

  prompt_length="${#APOLLO_LEFT_PROMPT_MODULES[@]}"

  for ((module=1; module<=prompt_length;module++)); do

    current_module="${APOLLO_LEFT_PROMPT_MODULES[module]}"

    if (( _APOLLO_MODULE_HAS_LENGTH[$current_module] > 0 )); then

      if ((first_skipped > 0)); then

        if [[ "$current_module" == "newline" || "$current_module" == "ruler" ]]; then
          first_skipped=0
          _apollo_set_colors "keep" "clear"
        else
          bg_var="APOLLO_${current_module}_BG"
          _apollo_set_colors "keep" "${(P)${bg_var}}"
        fi

        _APOLLO_LEFT_SEPARATORS[module]="${_APOLLO_RETURN_MESSAGE}${APOLLO_LEFT_SEPARATOR}"

      else
        if [[ "$current_module" != "newline" && "$current_module" != "ruler" ]]; then
          first_skipped=1
        fi
      fi

    fi

  done

  prompt_length="${#APOLLO_RIGHT_PROMPT_MODULES[@]}"

  for ((module=1; module<=prompt_length;module++)); do

    current_module="${APOLLO_RIGHT_PROMPT_MODULES[module]}"

    if [[ "$current_module" == "newline" || "$current_module" == "ruler" ]]; then
      continue;
    fi

    if (( _APOLLO_MODULE_HAS_LENGTH[$current_module] > 0 )); then
      bg_var="APOLLO_${current_module}_BG"
      _apollo_set_colors "${(P)${bg_var}}" "keep"
      _APOLLO_RIGHT_SEPARATORS[module]="${_APOLLO_RETURN_MESSAGE}${APOLLO_RIGHT_SEPARATOR}"
    fi

  done

}

_apollo_add_separators "$@"
