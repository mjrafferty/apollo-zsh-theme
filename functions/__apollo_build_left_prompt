# vim:ft=zsh

__apollo_build_left_prompt() {

  local module current_module prompt_end
  local -i line_index line
  local -a left_modules

  zstyle -a ":apollo:${__APOLLO_PROMPT_THEME}:core:modules:left" modules left_modules

  line_index=$(($#__APOLLO_PROMPT_LINES + 1));
  __APOLLO_PROMPT_THEME_START[$__APOLLO_PROMPT_THEME]="$line_index"

  __APOLLO_PROMPT_LINES[line_index]+="\${__APOLLO_LEFT_BEGIN[$line_index]}"

  for ((module=1; module<=${#left_modules[@]};module++)); do

    current_module="${left_modules[module]}"

    case "${current_module}" in
      "newline"|"ruler")
        __APOLLO_PROMPT_LINES[line_index]+="\${__APOLLO_LEFT_END[$line_index]}"

        __apollo_add_newline "${current_module}" "${line_index}"
        ((line_index++));

        # Initialize variables for new line
        __APOLLO_LEFT_LINE_MAP[line_index]=""
        __APOLLO_RIGHT_LINE_MAP[line_index]=""
        __APOLLO_LEFT_HAS_LENGTH[line_index]=""
        __APOLLO_RIGHT_HAS_LENGTH[line_index]=""
        __APOLLO_LEFT_BEGIN[line_index]=""
        __APOLLO_LEFT_END[line_index]=""
        __APOLLO_RIGHT_BEGIN[line_index]=""
        __APOLLO_RIGHT_END[line_index]=""
        __APOLLO_LEFT_LINK[line_index]=""
        __APOLLO_RIGHT_LINK[line_index]=""

        __APOLLO_PROMPT_LINES[line_index]+="\${__APOLLO_LEFT_BEGIN[$line_index]}"
        ;;
      *)
        __APOLLO_MODULE_MAP[__APOLLO_MODULE_INDEX]="$current_module"
        __APOLLO_REVERSE_MODULE_MAP[$current_module]+="${line_index}:left:${__APOLLO_MODULE_INDEX}\0"
        __APOLLO_LEFT_LINE_MAP[line_index]+="${__APOLLO_MODULE_INDEX}\0"

        __APOLLO_PROMPT_LINES[line_index]+="\${__APOLLO_LEFT_SEPARATORS[${__APOLLO_MODULE_INDEX}]}\${(e)__APOLLO_MODULE_TEXT[${__APOLLO_MODULE_INDEX}]}"
        __APOLLO_LEFT_HAS_LENGTH[line_index]+=" + \$__APOLLO_MODULE_HAS_LENGTH[${__APOLLO_MODULE_INDEX}]"
        ((__APOLLO_MODULE_INDEX++))
        ;;
    esac

  done

  for ((line=__APOLLO_PROMPT_THEME_START[$__APOLLO_PROMPT_THEME];line<=line_index;line++)); do

    if ((line == line_index)); then
      __APOLLO_PROMPT_LINES[$line]="\${__APOLLO_LEFT_LINK[$line]}${__APOLLO_PROMPT_LINES[$line]}"
    else
      __APOLLO_PROMPT_LINES[$line]="\${__APOLLO_LEFT_LINK[$line]}${__APOLLO_PROMPT_LINES[$line]}\${__APOLLO_RIGHT_LINK[$line]}"
    fi

    __APOLLO_FINISHED_PROMPTS[$__APOLLO_PROMPT_THEME]+="\${(e)__APOLLO_PROMPT_LINES[$line]}"

    if ((line < line_index)); then
      __APOLLO_FINISHED_PROMPTS[$__APOLLO_PROMPT_THEME]+=$'\n'
    fi

  done

  __APOLLO_PROMPT_THEME_END[$__APOLLO_PROMPT_THEME]="$line_index"

  __APOLLO_PROMPT_LINES[line_index]+="\${__APOLLO_LEFT_END[$line_index]}"

  __apollo_set_style ":apollo:${__APOLLO_PROMPT_THEME}:core:prompt:end"

  __APOLLO_FINISHED_PROMPTS[$__APOLLO_PROMPT_THEME]+="$__APOLLO_RETURN_MESSAGE%{%f%k%}"

}

__apollo_build_left_prompt "$@"
