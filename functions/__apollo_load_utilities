# vim:ft=zsh

__apollo_get_display_width(){

  local format_regex='%{*%}'

  __APOLLO_RETURN_MESSAGE="${(m)#${(S%)1//$~format_regex/}}"

}

__apollo_set_mode() {

  __APOLLO_MODULE_MODE[$1]="$2"

}

__apollo_cleanup() {

  exec {__APOLLO_BUFFER_FD}>&-
  rm "$__APOLLO_BUFFER_NAME"

  if (( __APOLLO_ASYNC == 1 )); then
    async_stop_worker apollo_async_helper
  fi

}

__apollo_chpwd() {

  ## Seeing bug $PWD not always updating. Hoping this will work around it
  PWD="${(%):-%d}"

  if ((__APOLLO_ASYNC == 1)); then
    async_worker_eval apollo_async_helper "cd $PWD"
  fi
}

## Update buffered prompt when executing command
zle-line-finish() {

  local current_theme="$__APOLLO_PROMPT_THEME"

  APOLLO_THEME="buffer"
  __apollo_run_prompt

  zle && zle .reset-prompt

  APOLLO_THEME="$current_theme"

}

__apollo_load_utilities() {};

__apollo_load_utilities "$@"
