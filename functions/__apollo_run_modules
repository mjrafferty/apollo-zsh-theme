# vim:ft=zsh

__apollo_run_modules() {

  local line module
  local start
  local -a left_modules right_modules

  zstyle -a 'apollo:core:modules:left' modules left_modules
  zstyle -a 'apollo:core:modules:right' modules right_modules

  if zstyle -m 'apollo:core:profiler' enabled true; then
    start=${EPOCHREALTIME}
  fi

  PROMPT="$__APOLLO_PROMPT_COPY"
  RPROMPT="$__APOLLO_RPROMPT_COPY"

  for module in "${right_modules[@]}"; do
    if ((${+functions[__apollo_${module}_run]} > 0)); then
      __apollo_get_module_text "$module" "right"
    fi
  done

  for module in "${left_modules[@]}"; do
    if ((${+functions[__apollo_${module}_run]} > 0)); then
      __apollo_get_module_text "$module" "left"
    fi
  done

  if zstyle -m 'apollo:core:profiler' enabled true; then
    printf "%25s: %f\n" "modules_total" "$((EPOCHREALTIME - start))"
  fi

}

__apollo_run_modules "$@"
