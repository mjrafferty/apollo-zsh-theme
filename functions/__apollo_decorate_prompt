# vim:ft=zsh

__apollo_decorate_prompt() {

  local start current_module left_sep right_sep
  local -i first_skipped=0
  local -i module
  local -a left_modules right_modules
  local mode="default"

  zstyle -a 'apollo:core:modules:left' modules left_modules
  zstyle -a 'apollo:core:modules:right' modules right_modules

  if zstyle -t 'apollo:core:profiler' enabled "true" "yes" "1" "on" ; then
    start="$EPOCHREALTIME"
  fi

  for ((module=1; module<=${#left_modules[@]};module++)); do

    current_module="${left_modules[module]}"

    if (( __APOLLO_MODULE_HAS_LENGTH[$current_module] > 0 )); then

      if ((first_skipped > 0)); then

        if [[ "$current_module" == "newline" || "$current_module" == "ruler" ]]; then
          first_skipped=0
        fi

        mode="${__APOLLO_MODULE_MODE[$current_module]}"

        zstyle -s "apollo:${current_module}:${mode}:left:separator" text left_sep
        __apollo_format_separator "apollo:${current_module}:${mode}:left:separator" "$left_sep" "left"
        __APOLLO_LEFT_SEPARATORS[module]="${__APOLLO_RETURN_MESSAGE}"

      else
        __APOLLO_LEFT_SEPARATORS[module]=""
        if [[ "$current_module" != "newline" && "$current_module" != "ruler" ]]; then
          first_skipped=1
        fi
      fi

    fi

  done

  for ((module=1; module<=${#right_modules[@]};module++)); do

    current_module="${right_modules[module]}"

    if [[ "$current_module" == "newline" || "$current_module" == "ruler" ]]; then
      continue;
    fi

    if (( __APOLLO_MODULE_HAS_LENGTH[$current_module] > 0 )); then

      mode="${__APOLLO_MODULE_MODE[$current_module]}"

      zstyle -s "apollo:${current_module}:${mode}:right:separator" text right_sep
      __apollo_format_separator "apollo:${current_module}:${mode}:right:separator" "$right_sep" "right"
      __APOLLO_RIGHT_SEPARATORS[module]="${__APOLLO_RETURN_MESSAGE}"
    else
      __APOLLO_RIGHT_SEPARATORS[module]=""
    fi

  done

  if zstyle -t 'apollo:core:profiler' enabled "true" "yes" "1" "on" ; then
    printf "%25s: %f\n" "separators" "$((EPOCHREALTIME-start))"
  fi

}

__apollo_decorate_prompt "$@"
