# vim:ft=zsh

__apollo_decorate_prompt() {

  local start module left_sep right_sep
  local -i first_skipped=0
  local -a line_modules
  local do_decorate
  local mode="default"

  zstyle -b "apollo:core:decorations" enabled do_decorate

  if [[ "$do_decorate" == "no" ]]; then
    return;
  fi

  if [[ "$profiler" == "yes" ]]; then
    start="$EPOCHREALTIME"
  fi

  for ((line=1; line <= ${#__APOLLO_PROMPT_LINES[@]}; line++)); do

    line_modules=(${(s:,:)__APOLLO_LEFT_LINE_MAP[line]})

    for num in "${line_modules[@]}"; do

      module="${__APOLLO_LEFT_MODULE_MAP[num]}"

      if (( __APOLLO_MODULE_HAS_LENGTH[$module] > 0 )); then

        if ((first_skipped > 0)); then

          if [[ "$module" == "newline" || "$module" == "ruler" ]]; then
            first_skipped=0
          fi

          mode="${__APOLLO_MODULE_MODE[$module]}"

          zstyle -s "apollo:${module}:${mode}:left:separator" text left_sep
          __apollo_format_separator "apollo:${module}:${mode}:left:separator" "$left_sep" "left"
          __APOLLO_LEFT_SEPARATORS[num]="${__APOLLO_RETURN_MESSAGE}"

        else
          __APOLLO_LEFT_SEPARATORS[num]=""
          if [[ "$module" != "newline" && "$module" != "ruler" ]]; then
            first_skipped=1
          fi
        fi

      fi

    done

    line_modules=(${(s:,:)__APOLLO_RIGHT_LINE_MAP[line]})

    for num in "${line_modules[@]}"; do

      module="${__APOLLO_RIGHT_MODULE_MAP[num]}"

      if [[ "$module" == "newline" || "$module" == "ruler" ]]; then
        continue;
      fi

      if (( __APOLLO_MODULE_HAS_LENGTH[$module] > 0 )); then

        mode="${__APOLLO_MODULE_MODE[$module]}"

        zstyle -s "apollo:${module}:${mode}:right:separator" text right_sep
        __apollo_format_separator "apollo:${module}:${mode}:right:separator" "$right_sep" "right"
        __APOLLO_RIGHT_SEPARATORS[num]="${__APOLLO_RETURN_MESSAGE}"
      else
        __APOLLO_RIGHT_SEPARATORS[num]=""
      fi

    done

  done

  if [[ "$profiler" == "yes" ]]; then
    printf "%25s: %f\n" "separators" "$((EPOCHREALTIME-start))"
  fi

}

__apollo_decorate_prompt "$@"
