# vim:ft=zsh

__apollo_decorate_prompt() {

  local start module
  local first_module last_module context last_context
  local -i line
  local -a line_modules
  local do_decorate

  zstyle -b ":apollo:core:decorations" enabled do_decorate

  if [[ "$do_decorate" == "no" ]]; then
    return;
  fi

  if [[ "$profiler" == "yes" ]]; then
    start="$EPOCHREALTIME"
  fi

  for ((line=1; line <= ${#__APOLLO_PROMPT_LINES[@]}; line++)); do

    __APOLLO_LEFT_BEGIN[line]=""
    __APOLLO_LEFT_END[line]=""

    line_modules=(${(s.\0.)__APOLLO_LEFT_LINE_MAP[line]})

    first_module=""
    last_module=""

    for num in "${line_modules[@]}"; do

      module="${__APOLLO_MODULE_MAP[num]}"

      if (( __APOLLO_MODULE_HAS_LENGTH[$num] > 0 )); then

        context=":apollo:${module}:${__APOLLO_MODULE_MODE[$module]:-default}:${line}:left"

        if [[ -n "$first_module" ]]; then

          __apollo_set_style "${context}:separator" "separator"
          __APOLLO_LEFT_SEPARATORS[num]="${__APOLLO_RETURN_MESSAGE}"

          last_module="$module"
          last_context="$context"

        else
          __APOLLO_LEFT_SEPARATORS[num]=""
          first_module="$module"
          last_module="$module"
          last_context="$context"

          __apollo_set_style "${context}:begin" "begin"
          __APOLLO_LEFT_BEGIN[line]="${__APOLLO_RETURN_MESSAGE}"

        fi

      else
        __APOLLO_LEFT_SEPARATORS[num]=""
      fi

    done

    if [[ -n "$last_module" ]]; then
      __apollo_set_style "${last_context}:end" "end"
      __APOLLO_LEFT_END[line]="${__APOLLO_RETURN_MESSAGE}"
    fi

    __APOLLO_RIGHT_BEGIN[line]=""
    __APOLLO_RIGHT_END[line]=""

    line_modules=(${(s.\0.)__APOLLO_RIGHT_LINE_MAP[line]})

    first_module=""
    last_module=""
    last_context=""

    for num in "${line_modules[@]}"; do

      module="${__APOLLO_MODULE_MAP[num]}"

      if (( __APOLLO_MODULE_HAS_LENGTH[$num] > 0 )); then

        context=":apollo:${module}:${__APOLLO_MODULE_MODE[$module]:-default}:${line}:right"

        if [[ -n "$first_module" ]]; then

          __apollo_set_style "${context}:separator" "separator"
          __APOLLO_RIGHT_SEPARATORS[num]="${__APOLLO_RETURN_MESSAGE}"

          last_module="$module"
          last_context="$context"

        else
          __APOLLO_RIGHT_SEPARATORS[num]=""
          first_module="$module"
          last_module="$module"
          last_context="$context"

          __apollo_set_style "${context}:begin" "begin"
          __APOLLO_RIGHT_BEGIN[line]="${__APOLLO_RETURN_MESSAGE}"

        fi

      else
        __APOLLO_RIGHT_SEPARATORS[num]=""
      fi

    done

    if [[ -n "$last_module" ]]; then
      __apollo_set_style "${last_context}:end" "end"
      __APOLLO_RIGHT_END[line]="${__APOLLO_RETURN_MESSAGE}"
    fi

    first_module=""
    last_module=""
    last_context=""

  done

  if [[ "$profiler" == "yes" ]]; then
    printf "%25s: %f\n" "decorations" "$((EPOCHREALTIME-start))"
  fi

}

__apollo_decorate_prompt "$@"
