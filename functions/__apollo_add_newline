# vim:ft=zsh

__apollo_add_newline() {

  local module faux_rprompt line ruler_text

  module="$1"
  line="$2"

  if [[ "${module}" == "newline" ]]; then
    __APOLLO_PROMPT_LINES[line]+="%{%k%}\${(er.\$((\$COLUMNS - \${__APOLLO_PROMPT_LINE_LENGTH[$line]})).. .)}"
  else

    zstyle -s ":apollo:${APOLLO_THEME}:core:${line}:ruler" text ruler_text

    # Using ¬ as a flag delimiter as it seems pretty uncommon and unlikely to be contained in the value of $ruler_text
    __apollo_set_style ":apollo:${APOLLO_THEME}:core:${line}:ruler" "\${(er¬\$((\$COLUMNS - \${(e)__APOLLO_PROMPT_LINE_LENGTH[$line]}))¬¬${ruler_text}¬)}"
    __APOLLO_PROMPT_LINES[line]+="${__APOLLO_RETURN_MESSAGE}"

    __APOLLO_LEFT_HAS_LENGTH[line]+=" + 1"
    __APOLLO_RIGHT_HAS_LENGTH[line]+=" + 1"

  fi

  __apollo_build_right_prompt "$line"
  faux_rprompt="${__APOLLO_RETURN_MESSAGE}"

  __APOLLO_PROMPT_LINES[line]+="${faux_rprompt}%{%f%k%}"

}

__apollo_add_newline "$@"

