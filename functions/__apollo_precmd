# vim:ft=zsh

__apollo_precmd() {

  __APOLLO_EXIT_CODE=$?
  __APOLLO_PIPE_EXIT_CODES=( "${pipestatus[@]}" )

  local start startmod profiler
	local line module
  local -a left_modules right_modules

  # Forcefully refresh cache by pressing enter a few times
  if ((__APOLLO_BREAK_CACHE_COUNTER++ >= 2)); then
    __APOLLO_CACHE=();
    __APOLLO_BREAK_CACHE_COUNTER=0;
  fi

  zstyle -b 'apollo:core:profiler' enabled profiler

  if [[ "$profiler" == "yes" ]] ; then
    start="$EPOCHREALTIME"
    startmod=${EPOCHREALTIME}
  fi

  zstyle -a 'apollo:core:modules:left' modules left_modules
  zstyle -a 'apollo:core:modules:right' modules right_modules

  PROMPT="$__APOLLO_PROMPT_COPY"
  RPROMPT="$__APOLLO_RPROMPT_COPY"

  for module in "${right_modules[@]}"; do
    case "$module" in
      "newline");;
      "ruler");;
      *)
        __apollo_get_module_text "$module" "right"
        ;;
    esac
  done

  for module in "${left_modules[@]}"; do
    case "$module" in
      "newline");;
      "ruler");;
      *)
        __apollo_get_module_text "$module" "left"
        ;;
    esac
  done

  if [[ "$profiler" == "yes" ]] ; then
    printf "%25s: %f\n" "modules_total" "$((EPOCHREALTIME - startmod))"
  fi

  __apollo_update_prompts

  if [[ "$profiler" == "yes" ]] ; then
    printf "%25s: %f\n" "total" "$((EPOCHREALTIME-start))"
  fi

}

__apollo_precmd "$@"
