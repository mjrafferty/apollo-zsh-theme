# vim:ft=zsh

_apollo_get_module_text() {

  local module side start cache_key cache runtime

  module="$1"
  side="$2"

  if ((APOLLO_PROFILER > 0)); then
    start=${EPOCHREALTIME}
  fi

  if ((${+functions[_apollo_${module}_cache_key]})); then
    "_apollo_${module}_cache_key";
    cache_key="${_APOLLO_RETURN_MESSAGE}"
    cache="${_APOLLO_CACHE["${module}-${cache_key}"]}"
  fi

  if [[ -n "$cache" ]]; then
    _APOLLO_MODULE_TEXT[$module]="${cache##*|}"
    _APOLLO_MODULE_HAS_LENGTH[$module]="${cache%%|*}"
  else
    _apollo_set_module_text "$module" "$side" "$cache_key"
  fi

  if (( _APOLLO_MODULE_HAS_LENGTH[$module] > 0 )); then
    _apollo_add_separator "$side" "$module"
    _APOLLO_MODULE_TEXT[$module]="${_APOLLO_RETURN_MESSAGE}${_APOLLO_MODULE_TEXT[$module]}"
  fi

  if ((APOLLO_PROFILER > 0)); then
    runtime="$((EPOCHREALTIME-start))"
    printf "%25s: %f\n" "$module" "$runtime"
    ((_APOLLO_PROFILER_TOTAL+=runtime))
  fi

}

_apollo_get_module_text "$@"
