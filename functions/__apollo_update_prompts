# vim:ft=zsh

__apollo_update_prompts() {

  local line
  local start

  if zstyle -t ":apollo:${__APOLLO_PROMPT_THEME}:core:links" enabled "true" "yes" "1" "on" ; then
    __apollo_add_links
  fi

  __apollo_decorate_prompt

  if [[ "$profiler" == "yes" ]]; then
    start="$EPOCHREALTIME"
  fi

  for ((line=__APOLLO_PROMPT_THEME_START[$__APOLLO_PROMPT_THEME];line<__APOLLO_PROMPT_THEME_END[$__APOLLO_PROMPT_THEME];line++)); do

    __APOLLO_PROMPT_LINE_LENGTH[line]="$COLUMNS"
    __apollo_get_display_width "${(e)__APOLLO_PROMPT_LINES[line]}"

    ## The 1 here is to avoid line overflow on older zsh versions.
    ((__APOLLO_PROMPT_LINE_LENGTH[line]=__APOLLO_RETURN_MESSAGE + 1));

  done

  if [[ "$profiler" == "yes" ]]; then
    printf "%25s: %f\n" "update length" "$((EPOCHREALTIME - start))"
  fi

}
__apollo_update_prompts "$@"
