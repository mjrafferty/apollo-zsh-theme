# vim:ft=zsh

__apollo_get_module_text() {

  local module side
  local -i line num
  local start
  local cache_key cache_status
  local -a cache

  module="$1"
  side="$2"
  line="$3"
  num="$4"

  if [[ "$profiler" == "yes" ]]; then
    start=${EPOCHREALTIME}
  fi

  if ((${+functions[__apollo_${module}_cache_key]})); then
    "__apollo_${module}_cache_key";
    cache_key="${side}-${line}-${num}-${__APOLLO_RETURN_MESSAGE}"
    cache=("${(s.\0.)__APOLLO_MODULE_CACHE["${cache_key}"]}")
  fi

  if [[ -n "$cache" ]]; then
    __APOLLO_MODULE_TEXT[$num]="${cache[2]}"
    __APOLLO_MODULE_HAS_LENGTH[$num]="${cache[1]}"
    __APOLLO_MODULE_MODE[$module]="${cache[3]}"
    cache_status="HIT"
  else
    __apollo_run_module "$module" "$line" "$side" "$num" "$cache_key"
    cache_status="MISS"
  fi

  if [[ "$profiler" == "yes" ]]; then
    printf "%25s: %f %s\n" "$module" "$((EPOCHREALTIME - start))" "$cache_status"
  fi

}

__apollo_get_module_text "$@"
