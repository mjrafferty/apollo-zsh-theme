# vim:ft=zsh

__apollo_get_module_text() {

  local module side line
  local start
  local cache_key cache_status
  local -a cache

  module="$1"
  side="$2"
  line="$3"

  if [[ "$profiler" == "yes" ]]; then
    start=${EPOCHREALTIME}
  fi

  if ((${+functions[__apollo_${module}_cache_key]})); then
    "__apollo_${module}_cache_key";
    cache_key="${__APOLLO_RETURN_MESSAGE}"
    cache=("${(s:,:)__APOLLO_CACHE["${module}-${cache_key}"]}")
  fi

  if [[ -n "$cache" ]]; then
    __APOLLO_MODULE_TEXT[$module]="${cache[2]}"
    __APOLLO_MODULE_HAS_LENGTH[$module]="${cache[1]}"
    __APOLLO_MODULE_MODE[$module]="${cache[3]}"
    cache_status="HIT"
  else
    __apollo_set_module_text "$module" "$side" "$line" "$cache_key"
    cache_status="MISS"
  fi

  if [[ "$profiler" == "yes" ]]; then
    printf "%25s: %f %s\n" "$module" "$((EPOCHREALTIME - start))" "$cache_status"
  fi

}

__apollo_get_module_text "$@"
