# vim:ft=zsh

__apollo_get_module_text() {

  local module side
  local -i line num
  local start
  local cache_key cache_status
  local -a cache

  line="$1"
  side="$2"
  module="$3"
  num="$4"

  if [[ "$profiler" == "yes" ]]; then
    start=${EPOCHREALTIME}
  fi

  if (( __APOLLO_ASYNC == 1 && ${+functions[__apollo_${module}_always_async]} )); then
    async_job apollo_async_helper "__apollo_${module}_always_async" ":apollo:${__APOLLO_PROMPT_THEME}:${line}:${side}:${module}"
  fi

  if ((${+functions[__apollo_${module}_cache_key]})); then
    "__apollo_${module}_cache_key";
    cache_key="${__APOLLO_PROMPT_THEME}-${line}-${side}-${num}-${__APOLLO_RETURN_MESSAGE}"
    cache=("${(s.\0.)__APOLLO_MODULE_CACHE["${cache_key}"]}")
  fi

  if [[ -n "$cache" ]]; then
    __APOLLO_MODULE_TEXT[$num]="${cache[2]}"
    __APOLLO_MODULE_HAS_LENGTH[$num]="${cache[1]}"
    __APOLLO_MODULE_MODE[$module]="${cache[3]}"
    cache_status="HIT"
  else

    if (( __APOLLO_ASYNC == 1 && ${+functions[__apollo_${module}_async]} )); then
      async_job apollo_async_helper "__apollo_${module}_async" ":apollo:${__APOLLO_PROMPT_THEME}:${line}:${side}:${module}"
    fi

    __apollo_run_module "$line" "$side" "$module" "$num" "$cache_key"
    cache_status="MISS"

  fi

  if [[ "$profiler" == "yes" ]]; then
    printf "%25s: %f %s\n" "$module" "$((EPOCHREALTIME - start))" "$cache_status"
  fi

}

__apollo_get_module_text "$@"
