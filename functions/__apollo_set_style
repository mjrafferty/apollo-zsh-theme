# vim:ft=zsh

__apollo_set_style() {

  local context text type cache
  local blend revblend
  local fg_color bg_color
  local left_label right_label

  context="$1"

  if [[ "$2" == "--"* ]]; then
    type="$2"
    shift;
  fi

  text="${(g::)2}"

  cache="${__APOLLO_STYLE_CACHE[$context]}"

  if [[ -z "$cache" ]]; then

    case "$type" in
      "--begin")
        zstyle -b "${context}" blend blend
        if [[ "$blend" == "yes" ]]; then
          zstyle -s "${context}" bg_color fg_color
          bg_color="clear"
        fi
        ;;
      "--end")
        zstyle -b "${context}" blend blend
        if [[ "$blend" == "yes" ]]; then
          fg_color="keep"
          bg_color="clear"
        fi
        ;;
      "--separator")
        zstyle -b "${context}" blend blend
        zstyle -b "${context}" revblend revblend
        if [[ "$blend" == "yes" ]]; then
          zstyle -s "${context}" bg_color bg_color
          fg_color="keep"
        elif [[ "$revblend" == "yes" ]]; then
          zstyle -s "${context}" bg_color fg_color
          bg_color="keep"
        fi
        ;;
      "--dynamic")
        text="\0"

        __apollo_format_string "${context}:left:label"
        left_label="$__APOLLO_RETURN_MESSAGE"

        __apollo_format_string "${context}:right:label"
        right_label="$__APOLLO_RETURN_MESSAGE"
        ;;
      *)
        __apollo_format_string "${context}:left:label"
        left_label="$__APOLLO_RETURN_MESSAGE"

        __apollo_format_string "${context}:right:label"
        right_label="$__APOLLO_RETURN_MESSAGE"
        ;;
    esac

    __apollo_format_string "${context}" "${text}" "${fg_color}" "${bg_color}"

    if [[ -z "$__APOLLO_RETURN_MESSAGE" ]]; then
      text=""
    else
      text="${left_label}${__APOLLO_RETURN_MESSAGE}${right_label}"
    fi

    __APOLLO_STYLE_CACHE[$context]="${text}"
    __APOLLO_RETURN_MESSAGE="${text}"

  else
    __APOLLO_RETURN_MESSAGE="${cache}"
  fi

}

__apollo_set_style "$@"
