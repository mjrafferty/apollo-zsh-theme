# vim:ft=zsh

__apollo_set_style() {

  local context text type cache
  local blend revblend
  local fg_color bg_color

  context="$1"
  type="$2"

  cache="${__APOLLO_STYLE_CACHE[$context]}"

  if [[ -z "$cache" ]]; then

    zstyle -s "${context}" text text

    case "$type" in
      "begin")
        zstyle -b "${context}" blend blend

        if [[ "$blend" == "yes" ]]; then
          zstyle -s "${context}:bg" color bg_color
          __apollo_format_string "${text}" "${context}" "${bg_color}" "clear"
        else
          __apollo_format_string "${text}" "${context}"
        fi
        ;;
      "end")
        zstyle -b "${context}" blend blend

        if [[ "$blend" == "yes" ]]; then
          __apollo_format_string "${text}" "${context}" "keep" "clear"
        else
          __apollo_format_string "${text}" "${context}"
        fi
        ;;
      "separator")
        zstyle -b "${context}" blend blend
        zstyle -b "${context}" revblend revblend

        if [[ "$blend" == "yes" ]]; then
          zstyle -s "${context}:bg" color bg_color
          __apollo_format_string "${text}" "${context}" "keep" "${bg_color}"
        elif [[ "$revblend" == "yes" ]]; then
          zstyle -s "${context}:bg" color fg_color
          __apollo_format_string "${text}" "${context}" "${fg_color}" "keep"
        else
          __apollo_format_string "${text}" "${context}"
        fi
        ;;
      "dynamic")
        __apollo_format_string ",," "${context}"
        ;;
      *)
        __apollo_format_string "${text}" "${context}"
        ;;
    esac

    text="${__APOLLO_RETURN_MESSAGE}"

    __APOLLO_STYLE_CACHE[$context]="${text}"
    __APOLLO_RETURN_MESSAGE="${text}"

  else
    __APOLLO_RETURN_MESSAGE="${cache}"
  fi

}

__apollo_set_style "$@"
