# vim:ft=zsh

__apollo_async_update() {

  local module="$1"
  local -i has_next="$2"
  local x side cache_key theme
  local -a module_info
  local -i line num change_made
  local line_before line_after
  local module_before module_after

  for x in "${(s.\0.)__APOLLO_REVERSE_MODULE_MAP[$module]}"; do

    module_info=("${(s.:.)x}")

    line="${module_info[1]}"
    side="${module_info[2]}"
    num="${module_info[3]}"
    theme="${module_info[4]}"

    # If this module isn't in the active theme, skip it
    if [[ "$theme" != "$__APOLLO_PROMPT_THEME" ]]; then
      continue;
    fi

    if ((line == 0)); then
      break;
    fi

    if ((${+functions[__apollo_${module}_cache_key]})); then
      "__apollo_${module}_cache_key";
      cache_key="${__APOLLO_PROMPT_THEME}-${line}-${side}-${num}-${__APOLLO_RETURN_MESSAGE}"
    fi

    case "$side" in
      "left")
        line_before="$(( ${(e)__APOLLO_LEFT_HAS_LENGTH[$line]} > 0 ))"
        ;;
      "right")
        line_before="$(( ${(e)__APOLLO_RIGHT_HAS_LENGTH[$line]} > 0 ))"
        ;;
    esac

    module_before="${__APOLLO_MODULE_HAS_LENGTH[$num]}"
    mode_before="${__APOLLO_MODULE_MODE[$module]}"

    __apollo_run_module "${line}" "${side}" "${module}" "${num}" "${cache_key}"

    module_after="${__APOLLO_MODULE_HAS_LENGTH[$num]}"
    mode_after="${__APOLLO_MODULE_MODE[$module]}"

    if [[ "$module_after" != "$module_before" || "$mode_after" != "$mode_before" ]]; then

      __apollo_decorate_prompt

      change_made=1

      case "$side" in
        "left")
          line_after="$(( ${(e)__APOLLO_LEFT_HAS_LENGTH[$line]} > 0 ))"
          ;;
        "right")
          line_after="$(( ${(e)__APOLLO_RIGHT_HAS_LENGTH[$line]} > 0 ))"
          ;;
      esac

      if ((line_after != line_before )); then
        __apollo_update_prompts
      fi

    fi

    __APOLLO_PROMPT_LINE_LENGTH[line]="$COLUMNS"
    __apollo_get_display_width "${(e)__APOLLO_PROMPT_LINES[line]}"

    ## The 1 here is to avoid line overflow on older zsh versions.
    ((__APOLLO_PROMPT_LINE_LENGTH[line]=__APOLLO_RETURN_MESSAGE + 1));

  done

  if (( has_next == 0 && change_made == 1 )); then
    zle && zle .reset-prompt
  fi

}

__apollo_async_update "$@"
