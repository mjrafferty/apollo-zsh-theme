# vim:ft=zsh

## Configurables
typeset -gi APOLLO_PROFILER=0
typeset -g  APOLLO_LEFT_SEPARATOR=""
typeset -g  APOLLO_RIGHT_SEPARATOR=""
typeset -g  APOLLO_PROMPT_END="> "
typeset -g  APOLLO_LINE_LINKS="true"
typeset -g  APOLLO_RULER_COLOR="white"

## Necessary global variables
typeset -gA _APOLLO_CONFIG
typeset -gA _APOLLO_CACHE
typeset -g 	_APOLLO_BUFFER_NAME
typeset -gi _APOLLO_BUFFER_FD
typeset -gA _APOLLO_MODULE_TEXT
typeset -gA _APOLLO_MODULE_HAS_LENGTH
typeset -ga _APOLLO_NEWLINE_SEP_TEXT
typeset -ga _APOLLO_PROMPT_LINE_LENGTH
typeset -g  _APOLLO_PROMPT_COPY # To prevent outside scripts messing with PS1/PROMPT
typeset -g  _APOLLO_RPROMPT_COPY # To prevent outside scripts messing with PS1/PROMPT
typeset -ga _APOLLO_LEFT_HAS_LENGTH
typeset -ga _APOLLO_RIGHT_HAS_LENGTH
typeset -ga _APOLLO_LEFT_SEPARATORS
typeset -ga _APOLLO_RIGHT_SEPARATORS
typeset -ga _APOLLO_LINE_MODULES
typeset -ga _APOLLO_PROMPT_LINES
typeset -gi _APOLLO_BREAK_CACHE_COUNTER
typeset -gi _APOLLO_CURRENT_RPROMPT=1
typeset -g  _APOLLO_PROFILER_TOTAL=0
typeset -gi _APOLLO_EXIT_CODE
typeset -g  _APOLLO_PIPE_EXIT_CODES
typeset -g  _APOLLO_RETURN_MESSAGE   # Used as a temporary storage for values to avoid using subshells

typeset -g  _APOLLO_NEWLINE='
'

_apollo_set_config () {

  local context key value

}

prompt_apollo_setup() {

  local module
  local -a left_modules right_modules

  setopt prompt_bang prompt_cr prompt_sp prompt_percent prompt_subst

  zmodload zsh/datetime

  # Load config files
  for conf in "${_APOLLO_INSTALL_DIR}/conf/"*.conf; do
    source "${conf}";
  done

  zstyle -a 'apollo:core:modules:left' modules left_modules
  zstyle -a 'apollo:core:modules:right' modules right_modules

  # TODO check fpath and update if necessary
  #fpath+=("${_APOLLO_INSTALL_DIR}/functions")
  fpath+=("${_APOLLO_INSTALL_DIR}/modules")

  # TODO move necessary utilities into functions
  source "${_APOLLO_INSTALL_DIR}/lib/utilities.zsh"

  autoload -Uz +X _apollo_add_links _apollo_add_separators _apollo_get_module_text \
    _apollo_precmd _apollo_run_modules _apollo_set_module_text _apollo_add_newline \
    _apollo_cleanup _apollo_make_rprompt _apollo_preexec _apollo_set_left_prompt \
    _apollo_set_prompts _apollo_set_colors _apollo_update_lengths _apollo_update_prompts \
    _apollo_async_update

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd _apollo_precmd
  add-zsh-hook preexec _apollo_preexec
  add-zsh-hook zshexit _apollo_cleanup

  # Open a buffer for modules to use
  _APOLLO_BUFFER_NAME="${TMPDIR:-/tmp}/${USER}_APOLLO_$EPOCHSECONDS"
  mkfifo "${_APOLLO_BUFFER_NAME}"
  exec {_APOLLO_BUFFER_FD}<> "${_APOLLO_BUFFER_NAME}"

  # Load enabled modules
  for module in "${left_modules[@]}" "${right_modules[@]}"; do

    if [[ "$module" == "newline" || "$module" == "ruler" ]]; then
      continue;
    fi

    autoload -Uz +X "_apollo_${module}_run"

  done

  PROMPT=""
  RPROMPT=""

  _apollo_set_prompts

  unfunction _apollo_add_newline _apollo_make_rprompt _apollo_set_left_prompt _apollo_set_prompts

}

prompt_apollo_setup "$@"
