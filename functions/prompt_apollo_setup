# vim:ft=zsh

typeset -g  APOLLO_THEME="${APOLLO_THEME:-apollo}"

typeset -g __APOLLO_PID="$$"

## Cache types
typeset -gA __APOLLO_MODULE_CACHE
typeset -gA __APOLLO_STYLE_CACHE

## Buffer data
typeset -g  __APOLLO_BUFFER_NAME
typeset -g  __APOLLO_BUFFER_FD

## Async data
typeset -gi __APOLLO_ASYNC
typeset -gA __APOLLO_ASYNC_RESULTS
typeset -gA __APOLLO_ASYNC_TIMES

## Utility variables
typeset -gi __APOLLO_BREAK_CACHE_COUNTER
typeset -gi __APOLLO_EXIT_CODE
typeset -gi __APOLLO_LEGACY_TRAP
typeset -g  __APOLLO_PIPE_EXIT_CODES
typeset -g  __APOLLO_RETURN_MESSAGE   # Used as a temporary storage for values to avoid using subshells

## Decorator variables
typeset -ga __APOLLO_LEFT_SEPARATORS
typeset -ga __APOLLO_RIGHT_SEPARATORS
typeset -ga __APOLLO_LEFT_BEGIN
typeset -ga __APOLLO_RIGHT_BEGIN
typeset -ga __APOLLO_LEFT_END
typeset -ga __APOLLO_RIGHT_END
typeset -ga __APOLLO_LEFT_LINK
typeset -ga __APOLLO_RIGHT_LINK

typeset -ga __APOLLO_MODULE_MAP
typeset -gA __APOLLO_REVERSE_MODULE_MAP
typeset -gA __APOLLO_MODULE_MODE
typeset -gA __APOLLO_MODULE_HAS_LENGTH
typeset -ga __APOLLO_MODULE_TEXT
typeset -gi __APOLLO_MODULE_INDEX=1

typeset -gA __APOLLO_PROMPT_THEME_START
typeset -gA __APOLLO_PROMPT_THEME_END

typeset -ga __APOLLO_PROMPT_LINES
typeset -ga __APOLLO_PROMPT_LINE_LENGTH
typeset -ga __APOLLO_LEFT_LINE_MAP
typeset -ga __APOLLO_RIGHT_LINE_MAP
typeset -ga __APOLLO_LEFT_HAS_LENGTH
typeset -ga __APOLLO_RIGHT_HAS_LENGTH

typeset -gA __APOLLO_FINISHED_PROMPTS
typeset -gA __APOLLO_FINISHED_RPROMPTS
typeset -gA __APOLLO_MODULES_LOADED

prompt_apollo_setup() {

  local conf

  setopt prompt_subst prompt_percent
  prompt_opts=(subst percent)

  zmodload zsh/datetime zsh/regex zsh/system zsh/mathfunc

__APOLLO_BUFFER_NAME="${TMPDIR:-/tmp}/${USER}__APOLLO_$EPOCHSECONDS"

  ## Set fpath ##
  if [[ -n "${__APOLLO_INSTALL_DIR}" ]]; then

    if [[ ${fpath[(ie)"${__APOLLO_INSTALL_DIR}/functions"]} -gt ${#fpath} ]]; then
      fpath+=("${__APOLLO_INSTALL_DIR}/functions.zwc" "${__APOLLO_INSTALL_DIR}/functions")
    fi

    if [[ ${fpath[(ie)"${__APOLLO_INSTALL_DIR}/modules"]} -gt ${#fpath} ]]; then
      fpath+=("${XDG_DATA_HOME:-${HOME}/.local/share}/apollo/" "${__APOLLO_INSTALL_DIR}/modules.zwc" "${__APOLLO_INSTALL_DIR}/modules")
    fi

    for conf in "${__APOLLO_INSTALL_DIR}/conf/"*.conf(N); do
      source "${conf}";
    done

  fi

  for conf in "${XDG_CONFIG_HOME:-${HOME}/.config}/apollo/"*.conf(N); do
    source "${conf}";
  done

  ## Load functions ##
  autoload -Uz +X __apollo_add_links __apollo_decorate_prompt __apollo_get_module_text \
    __apollo_precmd __apollo_run_module __apollo_add_newline __apollo_build_right_prompt \
    __apollo_preexec __apollo_build_left_prompt __apollo_set_colors __apollo_update_prompts \
    __apollo_async_update __apollo_format_string __apollo_style_module __apollo_set_style \
    __apollo_load_utilities __apollo_async_callback __apollo_init_theme __apollo_run_prompt \
    __apollo_async_runner

  __apollo_load_utilities;

  PROMPT=""
  RPROMPT=""

  autoload -Uz add-zsh-hook regexp-replace is-at-least

  add-zsh-hook precmd __apollo_precmd
  add-zsh-hook preexec __apollo_preexec
  add-zsh-hook chpwd __apollo_chpwd
  add-zsh-hook zshexit __apollo_cleanup
  zle -N zle-line-finish

  if ! is-at-least 5.0; then
    __APOLLO_LEGACY_TRAP=1
  fi

}

prompt_apollo_setup "$@"
