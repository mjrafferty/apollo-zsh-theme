# vim:ft=zsh

_apollo_set_left_prompt() {

  local module prompt_length current_module
  local -i line_index
  local -a left_modules right_modules

  zstyle -a 'apollo:core:modules:left' modules left_modules
  zstyle -a 'apollo:core:modules:right' modules right_modules

  line_index=1;
  prompt_length="${#left_modules[@]}"

  for ((module=1; module<=prompt_length;module++)); do

    current_module="${left_modules[module]}"

    _APOLLO_LINE_MODULES[line_index]+="${current_module} "

    case "${current_module}" in
      "newline")
        _APOLLO_PROMPT_LINES[line_index]+="\${_APOLLO_LEFT_SEPARATORS[$module]}"
        _apollo_add_newline "newline" "${line_index}"
        ((line_index++));
        _APOLLO_MODULE_HAS_LENGTH[$current_module]=1;
        ;;
      "ruler")
        _APOLLO_LEFT_HAS_LENGTH[line_index]+=" + 1"
        _APOLLO_RIGHT_HAS_LENGTH[line_index]+=" + 1"
        _APOLLO_PROMPT_LINES[line_index]+="\${_APOLLO_LEFT_SEPARATORS[$module]}"
        _apollo_add_newline "ruler" "${line_index}"
        ((line_index++));
        _APOLLO_MODULE_HAS_LENGTH[$current_module]=1;
        ;;
      *)
        _APOLLO_PROMPT_LINES[line_index]+="\${_APOLLO_LEFT_SEPARATORS[$module]}\${_APOLLO_MODULE_TEXT[$current_module]}"
        _APOLLO_LEFT_HAS_LENGTH[line_index]+=" + \$_APOLLO_MODULE_HAS_LENGTH[$current_module]"
        ;;
    esac

  done

  for ((line=1;line<=line_index;line++)); do

    if [[ "$APOLLO_LINE_LINKS" == "true" ]]; then
      if ((line < line_index)); then
        _APOLLO_PROMPT_LINES[$line]="\${_APOLLO_LEFT_LINK[$line]}${_APOLLO_PROMPT_LINES[$line]}\${_APOLLO_RIGHT_LINK[$line]}"
      else
        _APOLLO_PROMPT_LINES[$line]="\${_APOLLO_LEFT_LINK[$line]}${_APOLLO_PROMPT_LINES[$line]}"
      fi
    fi

    PROMPT+="\${(e)_APOLLO_PROMPT_LINES[$line]}"

    if ((line < line_index)); then
      PROMPT+="${_APOLLO_NEWLINE}"
    fi

  done

  PROMPT+="$APOLLO_PROMPT_END"
  _APOLLO_PROMPT_COPY="$PROMPT"

}

_apollo_set_left_prompt "$@"
