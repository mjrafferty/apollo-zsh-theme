# vim:ft=zsh

__apollo_format_string() {

  local style string context fg_color bg_color
  local -a styles

  context="$1"
  string="$2"
  fg_color="$3"
  bg_color="$4"

  if [[ -z "$string" ]]; then
    zstyle -s "${context}" text string
    string="${(g::)string}"
  fi

  if [[ -z "$string" ]]; then
    __APOLLO_RETURN_MESSAGE=""
    return;
  fi

  zstyle -a "$context" style styles

  for style in "${styles[@]}"; do
    case "$style" in
      "bold")
        string="%{%B%}${string}%{%b%}"
        ;;
      "standout")
        string="%{%S%}${string}%{%s%}"
        ;;
      "underline")
        string="%{%U%}${string}%{%u%}"
        ;;
      *);;
    esac
  done

  if [[ -z "$fg_color" ]]; then
    zstyle -s "${context}" fg_color fg_color
  fi

  if [[ -z "$bg_color" ]]; then
    zstyle -s "${context}" bg_color bg_color
  fi

  __apollo_set_colors "$fg_color" "$bg_color"


  __APOLLO_RETURN_MESSAGE="${__APOLLO_RETURN_MESSAGE}$string"

}

__apollo_format_string "$@"
