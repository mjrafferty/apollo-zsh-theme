# vim:ft=zsh

__apollo_add_separators() {

  local current_module
  local -i first_skipped=0
  local -i prompt_length module
  local -u bg_var
  local start
  local -a left_modules right_modules

  zstyle -a 'apollo:core:modules:left' modules left_modules
  zstyle -a 'apollo:core:modules:right' modules right_modules

  if (( APOLLO_PROFILER > 0 )); then
    start="$EPOCHREALTIME"
  fi

  if [[ -n "$APOLLO_LEFT_SEPARATOR" ]]; then

    prompt_length="${#left_modules[@]}"

    for ((module=1; module<=prompt_length;module++)); do

      current_module="${left_modules[module]}"

      if (( __APOLLO_MODULE_HAS_LENGTH[$current_module] > 0 )); then

        if ((first_skipped > 0)); then

          if [[ "$current_module" == "newline" || "$current_module" == "ruler" ]]; then
            first_skipped=0
            __apollo_set_colors "keep" "clear"
          else
            bg_var="APOLLO_${current_module}_BG"
            __apollo_set_colors "keep" "${(P)${bg_var}}"
          fi

          __APOLLO_LEFT_SEPARATORS[module]="${__APOLLO_RETURN_MESSAGE}${APOLLO_LEFT_SEPARATOR}"

        else
          __APOLLO_LEFT_SEPARATORS[module]=""
          if [[ "$current_module" != "newline" && "$current_module" != "ruler" ]]; then
            first_skipped=1
          fi
        fi

      fi

    done

  fi

  if [[ -n "$APOLLO_RIGHT_SEPARATOR" ]]; then

    prompt_length="${#right_modules[@]}"

    for ((module=1; module<=prompt_length;module++)); do

      current_module="${right_modules[module]}"

      if [[ "$current_module" == "newline" || "$current_module" == "ruler" ]]; then
        continue;
      fi

      if (( __APOLLO_MODULE_HAS_LENGTH[$current_module] > 0 )); then
        bg_var="APOLLO_${current_module}_BG"
        __apollo_set_colors "${(P)${bg_var}}" "keep"
        __APOLLO_RIGHT_SEPARATORS[module]="${__APOLLO_RETURN_MESSAGE}${APOLLO_RIGHT_SEPARATOR}"
      else
        __APOLLO_RIGHT_SEPARATORS[module]=""
      fi

    done

  fi

  if (( APOLLO_PROFILER > 0 )); then
    printf "%25s: %f\n" "separators" "$((EPOCHREALTIME-start))"
  fi

}

__apollo_add_separators "$@"
