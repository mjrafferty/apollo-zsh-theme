# vim:ft=zsh

__apollo_add_separators() {

  local start current_module left_sep right_sep
  local -i first_skipped=0
  local -i prompt_length module
  local -u bg_var
  local -a left_modules right_modules

  zstyle -a 'apollo:core:modules:left' modules left_modules
  zstyle -a 'apollo:core:modules:right' modules right_modules

  if zstyle -t 'apollo:core:profiler' enabled "true" "yes" "1" "on" ; then
    start="$EPOCHREALTIME"
  fi

  zstyle -s 'apollo:core:modules:left' separator left_sep

  if [[ -n "$left_sep" ]]; then

    prompt_length="${#left_modules[@]}"

    for ((module=1; module<=prompt_length;module++)); do

      current_module="${left_modules[module]}"

      if (( __APOLLO_MODULE_HAS_LENGTH[$current_module] > 0 )); then

        if ((first_skipped > 0)); then

          if [[ "$current_module" == "newline" || "$current_module" == "ruler" ]]; then
            first_skipped=0
            __apollo_set_colors "keep" "clear"
          else
            bg_var="APOLLO_${current_module}_BG"
            __apollo_set_colors "keep" "${(P)${bg_var}}"
          fi

          __APOLLO_LEFT_SEPARATORS[module]="${__APOLLO_RETURN_MESSAGE}${left_sep}"

        else
          __APOLLO_LEFT_SEPARATORS[module]=""
          if [[ "$current_module" != "newline" && "$current_module" != "ruler" ]]; then
            first_skipped=1
          fi
        fi

      fi

    done

  fi

  zstyle -s 'apollo:core:modules:right' separator right_sep

  if [[ -n "$right_sep" ]]; then

    prompt_length="${#right_modules[@]}"

    for ((module=1; module<=prompt_length;module++)); do

      current_module="${right_modules[module]}"

      if [[ "$current_module" == "newline" || "$current_module" == "ruler" ]]; then
        continue;
      fi

      if (( __APOLLO_MODULE_HAS_LENGTH[$current_module] > 0 )); then
        bg_var="APOLLO_${current_module}_BG"
        __apollo_set_colors "${(P)${bg_var}}" "keep"
        __APOLLO_RIGHT_SEPARATORS[module]="${__APOLLO_RETURN_MESSAGE}${right_sep}"
      else
        __APOLLO_RIGHT_SEPARATORS[module]=""
      fi

    done

  fi

  if zstyle -t 'apollo:core:profiler' enabled "true" "yes" "1" "on" ; then
    printf "%25s: %f\n" "separators" "$((EPOCHREALTIME-start))"
  fi

}

__apollo_add_separators "$@"
