# vim:ft=zsh

# Description:
#
#    Username and hostname
#
#       Modes: default sudo ssh
#    Elements: user separator host

__apollo_context_cache_key() {
  __APOLLO_RETURN_MESSAGE="1"
}

__apollo_context_run() {

  local -i line
  local side mode

  line="$1"
  side="$2"
  mode="default"

  local -a ignored_users ignored_hosts
  local user host ignore_user ignore_host
  local user_text sep_text host_text

  host_text="%m"
  user_text="%n"

  if [[ -n "$SSH_TTY" ]]; then
    mode="ssh"
    __apollo_set_mode "context" "ssh"
  elif [[ -n "$SUDO_USER" ]]; then
    mode="sudo"
    __apollo_set_mode "context" "sudo"
  fi

  zstyle -a ":apollo:context:${mode}:${line}:${side}" ignore_users ignored_users

  for user in "${ignored_users[@]}"; do
    if [[ "$USER" =~ $user ]]; then
      ignore_user=1;
    fi
  done

  zstyle -a ":apollo:context:${mode}:${line}:${side}" ignore_hosts ignored_hosts

  for host in "${ignored_hosts[@]}"; do
    if [[ "$HOST" =~ $host ]]; then
      ignore_host=1;
    fi
  done

  if ((ignore_host==1 && ignore_user==1)); then

    __APOLLO_RETURN_MESSAGE=""

  elif ((ignore_user==1)); then

    __apollo_set_style ":apollo:context:${mode}:${line}:${side}:host" "${host_text}"
    host_text="${__APOLLO_RETURN_MESSAGE}"

    __APOLLO_RETURN_MESSAGE="${host_text}"

  elif ((ignore_host==1)); then

    __apollo_set_style ":apollo:context:${mode}:${line}:${side}:user" "${user_text}"
    user_text="${__APOLLO_RETURN_MESSAGE}"

    __APOLLO_RETURN_MESSAGE="${user_text}"

  else

    __apollo_set_style ":apollo:context:${mode}:${line}:${side}:host" "${host_text}"
    host_text="${__APOLLO_RETURN_MESSAGE}"

    __apollo_set_style ":apollo:context:${mode}:${line}:${side}:user" "${user_text}"
    user_text="${__APOLLO_RETURN_MESSAGE}"

    __apollo_set_style ":apollo:context:${mode}:${line}:${side}:sep"
    sep="${__APOLLO_RETURN_MESSAGE}"

    __APOLLO_RETURN_MESSAGE="${user_text}${sep}${host_text}"

  fi


}
