# vim:ft=zsh

__apollo_status_cache_key(){
  __APOLLO_RETURN_MESSAGE="${__APOLLO_PIPE_EXIT_CODES}${__APOLLO_EXIT_CODE}"
}

__apollo_status_get_signal() {

  local code="$1"

  case "$code" in
    (126)
      __APOLLO_RETURN_MESSAGE="NOT_EXEC";
      ;;
    (127)
      __APOLLO_RETURN_MESSAGE="NOT_FOUND";
      ;;
    (130)
      __APOLLO_RETURN_MESSAGE="CTRL_C";
      ;;
    (*)
      if ((__APOLLO_EXIT_CODE > 128)); then
        if [[ -n "${signals[code-128]}" ]]; then
          __APOLLO_RETURN_MESSAGE="SIG${signals[code-128]}"
        else
          __APOLLO_RETURN_MESSAGE=""
        fi
      else
        __APOLLO_RETURN_MESSAGE=""
      fi
      ;;
  esac

}

__apollo_status_run() {

  local -i  error pipe
  local context mode pipe_status verbose always_show
  local -a error_text

  context="$1"
  mode="default"

  if (( $#__APOLLO_PIPE_EXIT_CODES > 1 )); then

    pipe=1;

    for ((code=1;code<=${#__APOLLO_PIPE_EXIT_CODES[@]};code++)); do
      if (( __APOLLO_PIPE_EXIT_CODES[code] > 0 )); then
        error=1;
        __apollo_status_get_signal "${__APOLLO_PIPE_EXIT_CODES[code]}"
        error_text[code]="$__APOLLO_RETURN_MESSAGE"
      fi
    done

  elif (( __APOLLO_EXIT_CODE > 0 )); then

    error=1;
    __apollo_status_get_signal "$__APOLLO_EXIT_CODE"
    error_text[1]="$__APOLLO_RETURN_MESSAGE"

  fi

  if (( error == 1 )); then

    __apollo_set_mode "status" "bad"
    mode="bad"

    zstyle -b "${context}:${mode}" simple simple

    if [[ "$simple" == "yes" ]]; then
      __APOLLO_RETURN_MESSAGE=""
      return;
    fi

    zstyle -b "${context}:${mode}" verbose verbose
    zstyle -b "${context}:${mode}" pipe_status pipe_status

    if [[ "${pipe}" == 1 && "$pipe_status" == "yes" ]]; then

      __APOLLO_RETURN_MESSAGE=""

      for ((code=1;code<=${#__APOLLO_PIPE_EXIT_CODES[@]};code++)); do

        if ((code > 1)); then
          __APOLLO_RETURN_MESSAGE+="|"
        fi

        if [[ "$verbose" == "yes" && -n "${error_text[code]}" ]]; then
          __APOLLO_RETURN_MESSAGE+="${error_text[code]}(${__APOLLO_PIPE_EXIT_CODES[code]})"
        else
          __APOLLO_RETURN_MESSAGE+="${__APOLLO_PIPE_EXIT_CODES[code]}"
        fi

      done

    else
      if [[ "$verbose" == "yes" && -n "${error_text[1]}" ]]; then
        __APOLLO_RETURN_MESSAGE="${error_text[1]}($__APOLLO_EXIT_CODE)"
      else
        __APOLLO_RETURN_MESSAGE="$__APOLLO_EXIT_CODE"
      fi
    fi

  else

    zstyle -b "${context}:${mode}" simple simple

    if [[ "$simple" == "yes" ]]; then
      __APOLLO_RETURN_MESSAGE=""
      return;
    fi

    zstyle -b "${context}:${mode}" always_show always_show
    zstyle -b "${context}:${mode}" simple simple

    if [[ "$always_show" == "yes" ]]; then

      zstyle -b "${context}:${mode}" verbose verbose

      if [[ "$verbose" == "yes" ]]; then
        __APOLLO_RETURN_MESSAGE="OK"
      else
        __APOLLO_RETURN_MESSAGE="0"
      fi

    else
      __APOLLO_RETURN_MESSAGE=""
    fi

  fi

}
