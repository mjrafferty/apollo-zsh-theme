# vim:ft=zsh

################################################################
# Context: user@hostname (who am I and where am I)
# Note that if $DEFAULT_USER is not set, this prompt module will always print

typeset -g APOLLO_CONTEXT_TEMPLATE="%n@%m"
typeset -g APOLLO_CONTEXT_ALWAYS_SHOW_USER="false"
typeset -g APOLLO_CONTEXT_ALWAYS_SHOW="false"

__apollo_context_cache_key() {
  __APOLLO_RETURN_MESSAGE="1"
}

__apollo_context_run() {

  local -i line
  local side user
  local -a ignored_users
  local ignore_user ignore_host
  local -a colors
  local show_host

  local host_text="%m"
  local user_text="%n"

  line="$1"
  side="$2"

  zstyle -a "apollo:context:*:${line}:${side}:user:ignore" users ignored_users

  for user in "${ignored_users[@]}"; do
    if [[ "$USER" == "$user" ]]; then
      ignore_user=1;
    fi
  done

  if [[ -z "$SSH_TTY" ]]; then

    zstyle -b "apollo:context:*:${line}:${side}:host" always_show show_host

    if [[ "$show_host" == "no" ]]; then
      ignore_host=1;
    fi

  fi

  if ((ignore_host==1 && ignore_user==1)); then

    __APOLLO_RETURN_MESSAGE=""

  elif ((ignore_user==1)); then

    __apollo_set_style "apollo:context:*:${line}:${side}:host" "dynamic"
    colors=("${(s:,:)__APOLLO_RETURN_MESSAGE[@]}")

    __APOLLO_RETURN_MESSAGE="${colors[1]}${host_text}${colors[2]}"

  elif ((ignore_host==1)); then

    __apollo_set_style "apollo:context:*:${line}:${side}:user" "dynamic"
    colors=("${(s:,:)__APOLLO_RETURN_MESSAGE[@]}")

    __APOLLO_RETURN_MESSAGE="${colors[1]}${user_text}${colors[2]}"

  else

    __apollo_set_style "apollo:context:*:${line}:${side}:host" "dynamic"
    colors=("${(s:,:)__APOLLO_RETURN_MESSAGE[@]}")
    host_text="${colors[1]}${host_text}${colors[2]}"

    __apollo_set_style "apollo:context:*:${line}:${side}:user" "dynamic"
    colors=("${(s:,:)__APOLLO_RETURN_MESSAGE[@]}")
    user_text="${colors[1]}${user_text}${colors[2]}"

    __APOLLO_RETURN_MESSAGE="${user_text}@${host_text}"

  fi


}

__apollo_context_run "$@"
