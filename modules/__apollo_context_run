# vim:ft=zsh

# Description:
#
#    Username and hostname
#
#       Modes: default sudo ssh
#    Elements: user separator host


typeset -g APOLLO_CONTEXT_TEMPLATE="%n@%m"
typeset -g APOLLO_CONTEXT_ALWAYS_SHOW_USER="false"
typeset -g APOLLO_CONTEXT_ALWAYS_SHOW="false"

__apollo_context_cache_key() {
  __APOLLO_RETURN_MESSAGE="1"
}

__apollo_context_run() {

  local -i line
  local side mode

  line="$1"
  side="$2"
  mode="default"

  local -a ignored_users
  local user ignore_user ignore_host
  local -a colors
  local show_host

  local host_text="%m"
  local user_text="%n"

  if [[ -n "$SUDO_USER" ]]; then
    mode="sudo"
    __apollo_set_mode "context" "sudo"
  elif [[ -n "$SSH_TTY" ]]; then
    mode="ssh"
    __apollo_set_mode "context" "ssh"
  fi

  zstyle -a "apollo:context:${mode}:${line}:${side}:user:ignore" users ignored_users

  for user in "${ignored_users[@]}"; do
    if [[ "$USER" == "$user" ]]; then
      ignore_user=1;
    fi
  done

  if [[ -z "$SSH_TTY" ]]; then

    zstyle -b "apollo:context:${mode}:${line}:${side}:host" always_show show_host

    if [[ "$show_host" == "no" ]]; then
      ignore_host=1;
    fi

  fi

  if ((ignore_host==1 && ignore_user==1)); then

    __APOLLO_RETURN_MESSAGE=""

  elif ((ignore_user==1)); then

    __apollo_set_style "apollo:context:${mode}:${line}:${side}:host" "default" "${host_text}"
    host_text="${__APOLLO_RETURN_MESSAGE}"

    __APOLLO_RETURN_MESSAGE="${host_text}"

  elif ((ignore_host==1)); then

    __apollo_set_style "apollo:context:${mode}:${line}:${side}:user" "default" "${user_text}"
    user_text="${__APOLLO_RETURN_MESSAGE}"

    __APOLLO_RETURN_MESSAGE="${user_text}"

  else

    __apollo_set_style "apollo:context:${mode}:${line}:${side}:host" "default" "${host_text}"
    host_text="${__APOLLO_RETURN_MESSAGE}"

    __apollo_set_style "apollo:context:${mode}:${line}:${side}:user" "default" "${user_text}"
    user_text="${__APOLLO_RETURN_MESSAGE}"

    __APOLLO_RETURN_MESSAGE="${user_text}@${host_text}"

  fi


}

__apollo_context_run "$@"
