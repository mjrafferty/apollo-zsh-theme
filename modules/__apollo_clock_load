# vim:ft=zsh

__apollo_clock_cache_key() {
  __APOLLO_RETURN_MESSAGE="1"
}

__apollo_clock_run() {

  local -i line
  local side mode

  line="$1"
  side="$2"
  mode="default"

	local -a elements misc_style
  local return_string verbose

  zstyle -a ":apollo:clock:${mode}:${line}:${side}" elements elements

  for element in "${elements[@]}"; do
    case "$element" in
      "12hour")
        zstyle -b ":apollo:clock:${mode}:${line}:${side}:${element}" verbose verbose
        if [[ "${verbose}" == "yes" ]]; then
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%l}"
        else
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%I}"
        fi
        return_string+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "24hour")
        zstyle -b ":apollo:clock:${mode}:${line}:${side}:${element}" verbose verbose
        if [[ "${verbose}" == "yes" ]]; then
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%k}"
        else
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%H}"
        fi
        return_string+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "min")
        __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%M}"
        return_string+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "sec")
        __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%S}"
        return_string+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "ampm")
        zstyle -b ":apollo:clock:${mode}:${line}:${side}:${element}" verbose verbose
        if [[ "${verbose}" == "yes" ]]; then
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%p}"
        else
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%P}"
        fi
        return_string+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "timezone")
        zstyle -b ":apollo:clock:${mode}:${line}:${side}:${element}" verbose verbose
        if [[ "${verbose}" == "yes" ]]; then
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%Z}"
        else
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%z}"
        fi
        return_string+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      *)
        if [[ -z "$misc_style" ]]; then
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:misc" --dynamic
          misc_style=(${(s:\0:)__APOLLO_RETURN_MESSAGE})
        fi
        return_string+="${misc_style[1]}${element}${misc_style[2]}"
        ;;
    esac
  done

  __APOLLO_RETURN_MESSAGE="${return_string}"

}

local live

zstyle -b ':apollo:clock:*:*:*' live live

if [[ "$live" == "yes" ]]; then
  TMOUT=1
  TRAPALRM(){ zle && zle .reset-prompt }
fi

unset live
