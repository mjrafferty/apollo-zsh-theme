# vim:ft=zsh

__apollo_clock_cache_key() {
  __APOLLO_RETURN_MESSAGE="1"
}

__apollo_clock_run() {

  local -i line
  local side mode

  line="$1"
  side="$2"
  mode="default"

	local -a elements
  local format verbose

  zstyle -a ":apollo:clock:${mode}:${line}:${side}" elements elements

  for element in "${elements[@]}"; do
    case "$element" in
      "12hour")
        zstyle -b ":apollo:clock:${mode}:${line}:${side}:${element}" verbose verbose
        if [[ "${verbose}" == "yes" ]]; then
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%l}"
        else
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%I}"
        fi
        format+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "24hour")
        zstyle -b ":apollo:clock:${mode}:${line}:${side}:${element}" verbose verbose
        if [[ "${verbose}" == "yes" ]]; then
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%k}"
        else
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%H}"
        fi
        format+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "min")
        __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%M}"
        format+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "sec")
        __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%S}"
        format+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "ampm")
        zstyle -b ":apollo:clock:${mode}:${line}:${side}:${element}" verbose verbose
        if [[ "${verbose}" == "yes" ]]; then
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%p}"
        else
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%P}"
        fi
        format+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "timezone")
        zstyle -b ":apollo:clock:${mode}:${line}:${side}:${element}" verbose verbose
        if [[ "${verbose}" == "yes" ]]; then
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%Z}"
        else
          __apollo_set_style ":apollo:clock:${mode}:${line}:${side}:${element}" "%D{%z}"
        fi
        format+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      *)
        format+="${element}"
        ;;
    esac
  done

  __APOLLO_RETURN_MESSAGE="${format}"

}

local live

zstyle -b ':apollo:clock:*:*:*' live live

if [[ "$live" == "yes" ]]; then
  TMOUT=1
  TRAPALRM(){ zle && zle .reset-prompt }
fi

unset live
