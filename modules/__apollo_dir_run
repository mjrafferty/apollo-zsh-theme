# vim:ft=zsh

# Description:
#

__apollo_dir_cache_key() {
  __APOLLO_RETURN_MESSAGE="$PWD"
}

__apollo_dir_run() {

  local -i line last_count shorten_length
  local side mode method absolute dir sep output element shorten_length
  local -a split_dir last_style element_style short_style

  line="$1"
  side="$2"
  mode="default"

  if [[ ! -w "$PWD" ]]; then
    mode="readonly";
    __apollo_set_mode dir "readonly"
  fi

  zstyle -b "apollo:dir:${mode}:${line}:${side}" absolute absolute
  zstyle -s "apollo:dir:${mode}:${line}:${side}" last_count last_count
  zstyle -s "apollo:dir:${mode}:${line}:${side}" shorten_length shorten_length
  zstyle -s "apollo:dir:${mode}:${line}:${side}" shorten_string shorten_string

  if [[ "$absolute" == "yes" ]]; then
    dir="${(%):-%${last_count}d}"
  else
    dir="${(%):-%${last_count}~}"
  fi

  __apollo_set_style "apollo:dir:${mode}:${line}:${side}:sep" "default"
  sep="${__APOLLO_RETURN_MESSAGE}"

  __apollo_set_style "apollo:dir:${mode}:${line}:${side}:element" "dynamic"
  element_style=("${(s:;:)__APOLLO_RETURN_MESSAGE}")

  __apollo_set_style "apollo:dir:${mode}:${line}:${side}:shortened" "dynamic"
  short_style=("${(s:;:)__APOLLO_RETURN_MESSAGE}")

  split_dir=("${(s:/:)dir}")

  for ((element=1; element < ${#split_dir[@]}; element++)); do

    before_length="${#split_dir[element]}"

    split_dir[element]="${(%):-%${shorten_length}>${shorten_string}>${split_dir[element]}}"

    if (( ${#split_dir[element]} < before_length)); then
      output+="${short_style[1]}${split_dir[element]}${short_style[2]}${sep}"
    else
      output+="${element_style[1]}${split_dir[element]}${element_style[2]}${sep}"
    fi

  done

  __apollo_set_style "apollo:dir:${mode}:${line}:${side}:last" "dynamic"
  last_style=("${(s:;:)__APOLLO_RETURN_MESSAGE}")

  output+="${last_style[1]}${split_dir[element]}${last_style[2]}"
  __APOLLO_RETURN_MESSAGE="$output"

}

local bookmark pattern bookmark_name
local -a bookmarks bookmark_patterns
local -A split_pattern

zstyle -a "apollo:dir:${mode}:${line}:${side}" bookmarks bookmarks

for bookmark in "${bookmarks[@]}"; do
  hash -d "${bookmark}"
done

zstyle -a "apollo:dir:${mode}:${line}:${side}" bookmark_patterns bookmark_patterns

for pattern in "${bookmark_patterns[@]}"; do

  split_pattern=("${(s:;:)pattern}")

  if [[ -n "${split_pattern[2]}" ]]; then
    for bookmark in ${pattern}; do
      hash -d "${bookmark##*/}=${bookmark}";
    done
  else
    for bookmark in ${pattern}; do
      bookmark_name="${bookmark%/html}";
      hash -d "${bookmark_name##*/}=${bookmark}";
    done
  fi

done

unset bookmark bookmarks pattern bookmark_patterns split_pattern

__apollo_dir_run "$@"
