# vim:ft=zsh

# Description:
#

__apollo_dir_cache_key() {
  __APOLLO_RETURN_MESSAGE="$PWD"
}

__apollo_dir_last() {

  local last_count

  zstyle -s "apollo:dir:${mode}:${line}:${side}" last_count last_count

  if [[ "$absolute" == "yes" ]]; then
    __APOLLO_RETURN_MESSAGE="${(%):-%${last_count}d}"
  else
    __APOLLO_RETURN_MESSAGE="${(%):-%${last_count}~}"
  fi

}

__apollo_dir_run() {

  local -i line
  local side mode method absolute dir sep output
  local -a split_dir last_style element_style

  line="$1"
  side="$2"
  mode="default"

  if [[ ! -w "$PWD" ]]; then
    mode="readonly";
    __apollo_set_mode dir "readonly"
  fi

  zstyle -s "apollo:dir:${mode}:${line}:${side}" method method
  zstyle -b "apollo:dir:${mode}:${line}:${side}" absolute absolute

  if [[ "$absolute" == "yes" ]]; then
    dir="${(%):-%d}"
  else
    dir="${(%):-%~}"
  fi

  case "$method" in
    "truncate")
      __apollo_dir_truncate
      dir="${__APOLLO_RETURN_MESSAGE}"
      ;;
    "unique")
      __apollo_dir_unique
      dir="${__APOLLO_RETURN_MESSAGE}"
      ;;
    "last")
      __apollo_dir_last
      dir="${__APOLLO_RETURN_MESSAGE}"
      ;;
    *)
      ;;
  esac

  __apollo_set_style "apollo:dir:${mode}:${line}:${side}:sep" "default"
  sep="${__APOLLO_RETURN_MESSAGE}"

  __apollo_set_style "apollo:dir:${mode}:${line}:${side}:element" "dynamic"
  element_style=("${(s:;:)__APOLLO_RETURN_MESSAGE}")

  split_dir=("${(s:/:)dir}")

  for ((element=1; element < ${#split_dir[@]}; element++)); do
    output+="${element_style[1]}${split_dir[element]}${element_style[2]}${sep}"
  done

  __apollo_set_style "apollo:dir:${mode}:${line}:${side}:last" "dynamic"
  last_style=("${(s:;:)__APOLLO_RETURN_MESSAGE}")

  output+="${last_style[1]}${split_dir[element]}${last_style[2]}"
  __APOLLO_RETURN_MESSAGE="$output"

}

local bookmark
local -a bookmarks

zstyle -a "apollo:dir:${mode}:${line}:${side}" bookmarks bookmarks

for bookmark in "${bookmarks[@]}"; do
  hash -d "${bookmark}"
done

unset bookmark bookmarks

__apollo_dir_run "$@"
