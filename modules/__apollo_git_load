# vim:ft=zsh

__apollo_git_cache_key() {
  __APOLLO_RETURN_MESSAGE="$PWD"
}

__apollo_git_run() {

  local line="$1"
  local side="$2"
  local async_results="$3"

  __APOLLO_RETURN_MESSAGE="$async_results"

}

__apollo_git_always_async() {

  local line="$1"
  local side="$2"
  local -a results_array elements
  local results object return_string element
  local repo_path git_dir commit_hash local_branch remote_branch ahead behind
  local -i modified untracked

  # .git/logs/refs/stash < stash count
  # --ignore-submodules
  # --short=8
  # tag=$(command git describe --tags --exact-match HEAD 2>/dev/null)
  results="$(git rev-parse --short=8 --show-toplevel --git-dir HEAD && git status --porcelain -b)"
  results_array=(${(f)results})

  repo_path="${results_array[1]}"
  git_dir="${repo_path}/${results_array[2]}"
  commit_hash="${results_array[3]}"
  branch_info="${results_array[4]}"
  local_branch="${results_array[4]}"
  remote_branch="${results_array[4]}"
  ahead="${results_array[4]}"
  behind="${results_array[4]}"

  local_branch="${local_branch##\#\# }"
  local_branch="${local_branch%...*}"

  remote_branch="${remote_branch##*...}"
  remote_branch="${remote_branch% \[*}"

  for object in ${results_array[5,-1]}; do
    if [[ "$object" == "??"* ]]; then
      (( untracked+=1 ))
    else
      (( modified+=1 ))
    fi
  done

  zstyle -a ":apollo:git:${mode}:${line}:${side}" elements elements

  for element in "${elements[@]}"; do
    case "$element" in
      "repo_path"|"commit_hash"|"local_branch"|"remote_branch"|"ahead"|"behind"|"modified"|"untracked")
        __apollo_set_style ":apollo:git:${mode}:${line}:${side}:${element}" "dynamic"
        element_style=(${(s:\0:)__APOLLO_RETURN_MESSAGE})
        return_string+="${element_style[1]}${(P)element}${element_style[2]}"
        ;;
      *)
        return_string+="${element}"
        ;;
    esac
  done

  echo "$return_string"

}
