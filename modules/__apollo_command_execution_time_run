# vim:ft=zsh
################################################################
# Display the duration the command needed to run.

typeset -gi APOLLO_COMMAND_EXECUTION_TIME_PRECISION=2

typeset -gF __APOLLO_COMMAND_DURATION
typeset -gF __APOLLO_TIMER_START

__apollo_command_execution_time_run() {

  __APOLLO_COMMAND_DURATION=$((EPOCHREALTIME - __APOLLO_TIMER_START))
  __APOLLO_TIMER_START=1e10

  local humanReadableDuration

  if (( __APOLLO_COMMAND_DURATION < APOLLO_COMMAND_EXECUTION_TIME_THRESHOLD )); then
    __APOLLO_RETURN_MESSAGE=""
    return
  fi

  if (( __APOLLO_COMMAND_DURATION < 60 )); then

    if ((APOLLO_COMMAND_EXECUTION_TIME_PRECISION == 0)); then
      typeset -i humanReadableDuration
    else
      typeset -F ${APOLLO_COMMAND_EXECUTION_TIME_PRECISION} humanReadableDuration
    fi

    humanReadableDuration=$__APOLLO_COMMAND_DURATION

  elif (( __APOLLO_COMMAND_DURATION < 3600 )); then

    humanReadableDuration=$(TZ=GMT; strftime '%M:%S' $(( int(rint(__APOLLO_COMMAND_DURATION)) )))

  else

    humanReadableDuration=$(TZ=GMT; strftime '%H:%M:%S' $(( int(rint(__APOLLO_COMMAND_DURATION)) )))

  fi

  __APOLLO_RETURN_MESSAGE="${humanReadableDuration}"

}

__apollo_command_execution_time_preexec(){
  __APOLLO_TIMER_START=$EPOCHREALTIME
}

__APOLLO_TIMER_START=1e10

zmodload zsh/mathfunc

__apollo_command_execution_time_run "$@"
