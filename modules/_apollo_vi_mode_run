# vim:ft=zsh
################################################################
# Vi Mode: show editing mode (NORMAL|INSERT|VISUAL)
#
# VISUAL mode is shown as NORMAL unless APOLLO_VI_VISUAL_MODE_STRING is explicitly set.
# Your ZSH version must be >= 5.3 if you set this parameter.

typeset -g APOLLO_VI_MODE_FG="white"
typeset -g APOLLO_VI_MODE_BG="grey30"

_apollo_vi_mode_cache_key() {
  _APOLLO_RETURN_MESSAGE="1"
}

_apollo_vi_mode_run() {

	_APOLLO_VI_MODE="INSERT"

  _APOLLO_RETURN_MESSAGE='${_APOLLO_VI_MODE}'

}

zle-keymap-select() {

  local last_mode="$_APOLLO_VI_MODE"

  case "$KEYMAP" in
    "vicmd")
      if ((REGION_ACTIVE == 1)); then
        _APOLLO_VI_MODE="VISUAL"
      else
        _APOLLO_VI_MODE="NORMAL"
      fi
      ;;
    "main")
      case $ZLE_STATE in
        *insert)
          _APOLLO_VI_MODE="INSERT"
          ;;
        *overwrite)
          _APOLLO_VI_MODE="REPLACE"
          ;;
      esac
      ;;
    *);;
  esac

  if [[ "$_APOLLO_VI_MODE" != "$last_mode" ]]; then
    _apollo_update_lengths
    zle && zle reset-prompt
  fi

}

zle-line-init() {

  local last_mode="$_APOLLO_VI_MODE"

	_APOLLO_VI_MODE="INSERT"

  if [[ "$_APOLLO_VI_MODE" != "$last_mode" ]]; then
    _apollo_update_lengths
    zle && zle reset-prompt
  fi
}

_apollo_vi_mode_visual_mode() {

  local last_mode="$_APOLLO_VI_MODE"

  _APOLLO_VI_MODE="VISUAL"

  zle && zle visual-mode

  if [[ "$_APOLLO_VI_MODE" != "$last_mode" ]]; then
    _apollo_update_lengths
    zle && zle reset-prompt
  fi

}

_apollo_vi_mode_cmd_mode() {

  local last_mode="$_APOLLO_VI_MODE"

  _APOLLO_VI_MODE="NORMAL"

  zle && zle deactivate-region

  if [[ "$_APOLLO_VI_MODE" != "$last_mode" ]]; then
    _apollo_update_lengths
    zle && zle reset-prompt
  fi
}

zle -N zle-keymap-select
zle -N zle-line-init

autoload -Uz is-at-least

if is-at-least 5.3; then
  zle -N _apollo_vi_mode_visual_mode
  bindkey -M vicmd v _apollo_vi_mode_visual_mode
  zle -N _apollo_vi_mode_cmd_mode
  bindkey -M visual '^[' _apollo_vi_mode_cmd_mode
fi

_apollo_vi_mode_run "$@"
