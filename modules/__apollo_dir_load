# vim:ft=zsh

# Description:
#

__apollo_dir_cache_key() {
  __APOLLO_RETURN_MESSAGE="$PWD"
}

__apollo_dir_run() {

  local -i line last_count
  local side mode method absolute dir sep output element shorten_length
  local -a split_dir last_style element_style short_style

  line="$1"
  side="$2"
  mode="default"

  if [[ ! -w "$PWD" ]]; then
    mode="readonly";
    __apollo_set_mode dir "readonly"
  fi

  zstyle -b "apollo:dir:${mode}:${line}:${side}" absolute absolute
  zstyle -s "apollo:dir:${mode}:${line}:${side}" last_count last_count
  zstyle -s "apollo:dir:${mode}:${line}:${side}" shorten_length shorten_length
  zstyle -s "apollo:dir:${mode}:${line}:${side}" shorten_string shorten_string

  __apollo_set_style "apollo:dir:${mode}:${line}:${side}:sep" "default"
  sep="${__APOLLO_RETURN_MESSAGE}"

  if [[ "$absolute" == "yes" ]]; then
    dir="${(%):-%${last_count}d}"
    if (( ${#dir} < ${#${(%):-%d}} )); then
      dir="../${dir}"
    fi
  else
    dir="${(%):-%${last_count}~}"
    if (( ${#dir} < ${#${(%):-%~}} )); then
      dir="../${dir}"
    fi
  fi

  if [[ "$shorten_length" == "auto" ]]; then

    dir_length="${#dir}"

    if (( dir_length < 30 )); then
      shorten_length=0;
    elif (( dir_length < 60 )); then
      shorten_length=5;
    elif (( dir_length < 80 )); then
      shorten_length=3;
    else
      shorten_length=1;
    fi

  fi
  __apollo_set_style "apollo:dir:${mode}:${line}:${side}:sep" "default"
  sep="${__APOLLO_RETURN_MESSAGE}"

  __apollo_set_style "apollo:dir:${mode}:${line}:${side}:element" "dynamic"
  element_style=("${(s:;:)__APOLLO_RETURN_MESSAGE}")

  __apollo_set_style "apollo:dir:${mode}:${line}:${side}:shortened" "dynamic"
  short_style=("${(s:;:)__APOLLO_RETURN_MESSAGE}")

  split_dir=("${(s:/:)dir}")

  for ((element=1; element < ${#split_dir[@]}; element++)); do

    if (( element == 1 )); then
      output+="${element_style[1]}${split_dir[element]}${element_style[2]}${sep}"
      continue;
    fi

    before_length="${#split_dir[element]}"

    split_dir[element]="${(%):-%${shorten_length}>${shorten_string}>${split_dir[element]}}"

    if (( ${#split_dir[element]} < before_length)); then
      output+="${short_style[1]}${split_dir[element]}${short_style[2]}${sep}"
    else
      output+="${element_style[1]}${split_dir[element]}${element_style[2]}${sep}"
    fi

  done

  __apollo_set_style "apollo:dir:${mode}:${line}:${side}:last" "dynamic"
  last_style=("${(s:;:)__APOLLO_RETURN_MESSAGE}")

  output+="${last_style[1]}${split_dir[element]}${last_style[2]}"
  __APOLLO_RETURN_MESSAGE="$output"

}


local bookmark pattern bookmark_name
local -a bookmarks bookmark_patterns split_pattern

zstyle -a "apollo:dir:${mode}:${line}:${side}" bookmarks bookmarks

for bookmark in "${bookmarks[@]}"; do
  hash -d "${bookmark}" 2> /dev/null
done

zstyle -a "apollo:dir:${mode}:${line}:${side}" bookmark_patterns bookmark_patterns

for pattern in "${bookmark_patterns[@]}"; do

  split_pattern=("${(s:;:)pattern}")

  bookmarks=(${~=split_pattern[1]}(N))

  if [[ -z "${split_pattern[2]}" ]]; then
    for bookmark in "${bookmarks[@]}"; do
      hash -d "${bookmark##*/}=${bookmark}" 2> /dev/null
    done
  else
    for bookmark in "${bookmarks[@]}"; do
      bookmark_name="${bookmark%${split_pattern[2]}}"
      hash -d "${bookmark_name##*/}=${bookmark}" 2> /dev/null
    done
  fi

done

unset bookmark bookmarks pattern bookmark_patterns split_pattern
