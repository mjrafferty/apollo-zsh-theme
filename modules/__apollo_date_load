# vim:ft=zsh

__apollo_date_cache_key() {
  __APOLLO_RETURN_MESSAGE="${(%):-%D}"
}

__apollo_date_run() {

  local context mode

  context="$1"
  mode="default"

  local -a elements misc_style
  local return_string verbose

  zstyle -a "${context}:${mode}" elements elements

  for element in "${elements[@]}"; do
    case "$element" in
      "dow")
        zstyle -b "${context}:${mode}:${element}" verbose verbose
        if [[ "${verbose}" == "yes" ]]; then
          __apollo_set_style "${context}:${mode}:${element}" "%D{%A}"
        else
          __apollo_set_style "${context}:${mode}:${element}" "%D{%a}"
        fi
        return_string+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "day")
        zstyle -b "${context}:${mode}:${element}" verbose verbose
        if [[ "${verbose}" == "yes" ]]; then
          __apollo_set_style "${context}:${mode}:${element}" "%D{%e}"
        else
          __apollo_set_style "${context}:${mode}:${element}" "%D{%d}"
        fi
        return_string+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "month")
        zstyle -b "${context}:${mode}:${element}" verbose verbose
        if [[ "${verbose}" == "yes" ]]; then
          __apollo_set_style "${context}:${mode}:${element}" "%D{%B}"
        else
          __apollo_set_style "${context}:${mode}:${element}" "%D{%m}"
        fi
        return_string+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      "year")
        zstyle -b "${context}:${mode}:${element}" verbose verbose
        if [[ "${verbose}" == "yes" ]]; then
          __apollo_set_style "${context}:${mode}:${element}" "%D{%Y}"
        else
          __apollo_set_style "${context}:${mode}:${element}" "%D{%y}"
        fi
        return_string+="${__APOLLO_RETURN_MESSAGE}"
        ;;
      *)
        if [[ -z "$misc_style" ]]; then
          __apollo_set_style "${context}:${mode}:misc" --dynamic
          misc_style=(${(s:\0:)__APOLLO_RETURN_MESSAGE})
        fi
        return_string+="${misc_style[1]}${element}${misc_style[2]}"
        ;;
    esac
  done

  __APOLLO_RETURN_MESSAGE="${return_string}"

}
